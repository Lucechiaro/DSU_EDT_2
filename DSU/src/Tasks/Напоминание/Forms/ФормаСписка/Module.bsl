#Область ОписаниеПеременных

&НаКлиенте
Перем Отбор;
&НаКлиенте
Перем ИспользованиеОтбора;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РаскраситьЗавершенныеЗадачи();	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	//раскрасим выполненные задачи
	УстановитьОтборПоАктивности(ИспользованиеОтбора);
	
КонецПроцедуры


&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ВыполнениеЗадачи" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// управляет видимостью задач путём пеерключения режимов: отображение всех задач или отображени только активных задач
&НаКлиенте
Процедура ОбновитьСписокЗадач(Команда)
	
	ИспользованиеОтбора = Не ИспользованиеОтбора;
	УстановитьОтборПоАктивности(ИспользованиеОтбора);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
// устанавливает режим использования отбора задач по активности
Процедура УстановитьОтборПоАктивности(ИспользованиеОтбора)
	
	Если Отбор = Неопределено Тогда
		Отбор = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Выполнена");
		Отбор.ПравоеЗначение = Ложь;
	КонецЕсли;
	
	Отбор.Использование = ИспользованиеОтбора;
	КоманднаяПанель.ПодчиненныеЭлементы.ФормаПоказатьЗадачи.Заголовок = ?(ИспользованиеОтбора, "Показать все", "Показать активные");
	
КонецПроцедуры	

&НаСервере
// раскрашивает строки выполненных задач в радикальный бледно-зеленый цвет
Процедура РаскраситьЗавершенныеЗадачи()
	
	Оформление = Список.УсловноеОформление.Элементы.Добавить();
	
	Отбор = Оформление.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	Отбор.Использование = Истина;
	Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Выполнена");
	Отбор.ПравоеЗначение = Истина;
	
	ЦветФона = Оформление.Оформление.Элементы.Найти("ЦветФона");
	ЦветФона.Значение = WebЦвета.БледноЗеленый;
	ЦветФона.Использование = Истина;
	
КонецПроцедуры

#КонецОбласти

//@skip-check module-structure-top-region
#Область Инициализация

ИспользованиеОтбора = Истина;

#КонецОбласти
