#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Счет", Счет);
	
	Если ЗначениеЗаполнено(Счет) Тогда
		ФорматВыписки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Счет, "ФорматВыписки")
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СчетПриИзменении(Элемент)
	
	РеквизитыСчета = ПолучитьРеквизитыСчета(Счет);
	ФорматВыписки = РеквизитыСчета.ФорматВыписки;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрочитатьВыписку(Команда)
	
	Если ФорматВыписки.Пустая() Тогда
		
		ПоказатьПредупреждение(, "Укажите формат выписки!");
		Возврат;
		
	КонецЕсли;	
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ООВыборФайла = Новый ОписаниеОповещения("ООВыборФайлаЗавершение", ЭтотОбъект);
	Диалог.Показать(ООВыборФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьТранзакции(Команда)
	
	СохранитьТранзакцииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Асинх Процедура ООВыборФайлаЗавершение(ИменаФайлов, ДополнительныеСвойства) Экспорт
	
	Если ИменаФайлов <> Неопределено И ИменаФайлов.Количество() > 0 Тогда
		
		ИмяФайла = ИменаФайлов[0];
		РасширениеФайла = РасширениеФайла(ИмяФайла);
		
		#Если ВебКлиент Тогда
			
			УдалятьВременныйФайл = Ложь;
			ИмяВременногоФайла = ИмяФайла;
			
		#Иначе	
			УдалятьВременныйФайл = Истина;
			//@skip-check missing-temporary-file-deletion
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла(РасширениеФайла);
			ОбещаниеСкопироватьФайл = КопироватьФайлАсинх(ИмяФайла, ИмяВременногоФайла);
			
			Попытка
				ИмяВременногоФайла = Ждать ОбещаниеСкопироватьФайл;
			Исключение
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не удалось создать временный файл выписки!");
			КонецПопытки;		
			
		#КонецЕсли		
		
		ДвоичныеДанные = Новый ДвоичныеДанные(ИмяВременногоФайла); 
		АдресВХ = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
		ПрочитатьВыпискуНаСервере(АдресВХ, РасширениеФайла);
		
		Если УдалятьВременныйФайл Тогда
			УдалитьФайлыАсинх(ИмяВременногоФайла);
		КонецЕсли;	
		
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Процедура ПрочитатьВыпискуНаСервере(АдресВХ, РасширениеФайла)
	
	ДвоичныеДанные 	= ПолучитьИзВременногоХранилища(АдресВХ);
	ИмяФайла 		= ПолучитьИмяВременногоФайла(РасширениеФайла);
	ДвоичныеДанные.Записать(ИмяФайла);
	
	ИмяОбщегоМодуля = "РазборБанковскойВыписки" + ОбщегоНазначения.ИмяЗначенияПеречисления(ФорматВыписки);
	МодульРазбора = ОбщегоНазначения.ОбщийМодуль(ИмяОбщегоМодуля);
	РезультатыРазбора = РеквизитФормыВЗначение("ТаблицаТранзакций");
	МодульРазбора.РазобратьВыписку(РезультатыРазбора, ИмяФайла);
	ЗначениеВРеквизитФормы(РезультатыРазбора, "ТаблицаТранзакций");
	
	ЗаполнитьВалюты();
	ЗаполнитьСуществующиеТранзакции();	
	УдалитьФайлы(ИмяФайла);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьРеквизитыСчета(Счет)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Счета.ФорматВыписки
	|ИЗ
	|	Справочник.Счета КАК Счета
	|ГДЕ
	|	Счета.Ссылка = &Счет";
	Запрос.УстановитьПараметр("Счет", Счет);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Результат = Новый Структура("ФорматВыписки");
	
	Если Выборка.Следующий() Тогда
		Результат.ФорматВыписки = Выборка.ФорматВыписки;
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура СохранитьТранзакцииНаСервере()
	
	Для Каждого СтрокаТранзакции Из ТаблицаТранзакций Цикл
		
		Если СтрокаТранзакции.ДокументТранзакции.Пустая() Тогда
			ДокументТранзакции = Документы.БанковскаяТранзакция.СоздатьДокумент();
		Иначе
			ДокументТранзакции = СтрокаТранзакции.ДокументТранзакции.ПолучитьОбъект();
		КонецЕсли;		
		 
		 ЗаполнитьТранзакцию(ДокументТранзакции, СтрокаТранзакции);
		 
		 ДокументТранзакции.ПометкаУдаления = Ложь;
		 ДокументТранзакции.Записать();
		 СтрокаТранзакции.ДокументТранзакции = ДокументТранзакции.Ссылка;
		
	КонецЦикла;	
	
КонецПроцедуры	
	
&НаСервере
Процедура ЗаполнитьСуществующиеТранзакции()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ТаблицаТранзакций.ИдентификаторТранзакции
	|ПОМЕСТИТЬ ИдентификаторыТранзакций
	|ИЗ
	|	&ТаблицаТранзакций КАК ТаблицаТранзакций
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БанковскаяТранзакция.Ссылка,
	|	БанковскаяТранзакция.ИдентификаторТранзакции
	|ИЗ
	|	ИдентификаторыТранзакций КАК ИдентификаторыТранзакций
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.БанковскаяТранзакция КАК БанковскаяТранзакция
	|		ПО БанковскаяТранзакция.ИдентификаторТранзакции = ИдентификаторыТранзакций.ИдентификаторТранзакции";
	Запрос.УстановитьПараметр("ТаблицаТранзакций", ТаблицаТранзакций.Выгрузить());
	Выборка = Запрос.Выполнить().Выбрать();
	
	Отбор = Новый Структура("ИдентификаторТранзакции");
	
	Пока Выборка.Следующий() Цикл
		
		Отбор.ИдентификаторТранзакции = Выборка.ИдентификаторТранзакции;
		СтрокиТаблицы = ТаблицаТранзакций.НайтиСтроки(Отбор);
		
		Если СтрокиТаблицы.Количество() > 0 Тогда
			СтрокиТаблицы[0].ДокументТранзакции = Выборка.Ссылка;
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры		

&НаСервере
Процедура ЗаполнитьТранзакцию(ДокументТранзакции, СтрокаТранзакции)
	
	ЗаполнитьЗначенияСвойств(ДокументТранзакции, СтрокаТранзакции);
	ДокументТранзакции.Дата = СтрокаТранзакции.ДатаПоБанку;
	ДокументТранзакции.Счет = Счет;
	
КонецПроцедуры	
	
&НаСервере
Процедура ЗаполнитьВалюты()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Валюты.Ссылка,
	|	Валюты.КодВалюты
	|ИЗ
	|	Справочник.Валюты КАК Валюты";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Валюты = Новый Соответствие;
	
	Пока Выборка.Следующий() Цикл
		Валюты.Вставить(СокрЛП(Выборка.КодВалюты), Выборка.Ссылка);
	КонецЦикла;	
	
	Для Каждого СтрокаТранзакции Из ТаблицаТранзакций Цикл
		СтрокаТранзакции.Валюта = Валюты.Получить(СтрокаТранзакции.КодВалюты);
	КонецЦикла;	
		
КонецПроцедуры	

&НаКлиенте
Функция РасширениеФайла(ПутьКФайлу)
	
	ПозицияТочки = СтрНайти(ПутьКФайлу, ".", НаправлениеПоиска.СКонца);
	Расширение = Прав(ПутьКФайлу, СтрДлина(ПутьКФайлу) - ПозицияТочки);
	
	Возврат Расширение;
	
КонецФункции

#КонецОбласти