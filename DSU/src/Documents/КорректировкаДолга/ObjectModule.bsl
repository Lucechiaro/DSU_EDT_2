#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)

	Если ВидОперации = Перечисления.ВидыДолговыхОпераций.СписаниеДолга Тогда
		Движение = Движения.ДолговыеОбязательства.ДобавитьРасход();
		ЗаполнитьЗначенияСвойств(Движение, ПолучитьДанныеДолга(Ссылка, "Списания"));
	ИначеЕсли ВидОперации = Перечисления.ВидыДолговыхОпераций.УвеличениеДолга Тогда
		Движение = Движения.ДолговыеОбязательства.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(Движение, ПолучитьДанныеДолга(Ссылка, "Увеличения"));
	Иначе
		Движение = Движения.ДолговыеОбязательства.ДобавитьРасход();
		ЗаполнитьЗначенияСвойств(Движение, ПолучитьДанныеДолга(Ссылка, "Списания"));
		Движение = Движения.ДолговыеОбязательства.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(Движение, ПолучитьДанныеДолга(Ссылка, "Увеличения"));
	КонецЕсли;	
		
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;		
	
	Дата = НачалоДня(Дата);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДанныеДолга(Документ, ВидОперацииСтрокой)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	КорректировкаДолга.Должник#ВидОперации# КАК Должник,
	|	КорректировкаДолга.Валюта#ВидОперации# КАК Валюта,
	|	КорректировкаДолга.Сумма#ВидОперации# КАК Сумма,
	|	КорректировкаДолга.СрокИсполнения,
	|	КорректировкаДолга.Дата Как Период,
	|	КорректировкаДолга.Комментарий
	|ИЗ
	|	Документ.КорректировкаДолга КАК КорректировкаДолга
	|ГДЕ
	|	КорректировкаДолга.Ссылка = &Документ";
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "#ВидОперации#", ВидОперацииСтрокой);
	Запрос.УстановитьПараметр("Документ", Документ);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка
	
КонецФункции

#КонецОбласти

#КонецЕсли
