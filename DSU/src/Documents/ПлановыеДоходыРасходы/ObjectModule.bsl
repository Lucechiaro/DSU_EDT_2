#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;	
	
	Если ПериодПланирования = 0 Тогда
		ПредставлениеПериода = Формат(ДатаПланирования, "ДФ='d MMMM yyyy'");
	ИначеЕсли ПериодПланирования = 1 Тогда
		ПредставлениеПериода = Строка(НомерДня) + "-й день недели";
	ИначеЕсли ПериодПланирования = 2 Тогда
		ПредставлениеПериода = Строка(НомерДня) + "-е число месяца";
	ИначеЕсли ПериодПланирования = 3 Тогда
		ПредставлениеПериода = Формат(ДатаПланирования, "ДФ='d MMMM'");;
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Перем РасчетнаяДатаНачала, РасчетнаяДатаОкончания;
	
	Если ЗначениеЗаполнено(ДатаНачала) Тогда
		РасчетнаяДатаНачала = ДатаНачала;
	ИначеЕсли ЗначениеЗаполнено(Дата) Тогда
		РасчетнаяДатаНачала = НачалоДня(Дата);
	Иначе
		РасчетнаяДатаНачала = НачалоДня(ТекущаяДатаСеанса());
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаОкончания) Тогда
		РасчетнаяДатаОкончания = ДатаОкончания;
	Иначе
		РасчетнаяДатаОкончания = НачалоДня(ДобавитьМесяц(ТекущаяДатаСеанса(), 600));
	КонецЕсли;
	
	Движения.ГрафикПлановыхДоходовРасходов.Очистить();
	Движения.ГрафикПлановыхДоходовРасходов.Записывать = Истина;
	
	Если ПериодПланирования = 0 Тогда  // однократно (один день)
		
		НоваяСтрока = Движения.ГрафикПлановыхДоходовРасходов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭтотОбъект, "ВидДвижения,Статья,Сумма,Комментарий");
		НоваяСтрока.Период = ДатаПланирования;
		
	ИначеЕсли ПериодПланирования = 1 Тогда	// неделя
		
		СтартоваяДата = НачалоНедели(РасчетнаяДатаНачала) + НомерДня*86400;
		
		Если СтартоваяДата < РасчетнаяДатаНачала Тогда
			ДатаГрафика = СтартоваяДата + 7*86400;
		Иначе
			ДатаГрафика = СтартоваяДата;
		КонецЕсли;
		
		Пока ДатаГрафика <= РасчетнаяДатаОкончания Цикл
			
			НоваяСтрока = Движения.ГрафикПлановыхДоходовРасходов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭтотОбъект, "ВидДвижения,Статья,Сумма,Комментарий");
			НоваяСтрока.Период = ДатаГрафика;
			ДатаГрафика = ДатаГрафика + 7*86400;
			
		КонецЦикла;	
				
	ИначеЕсли ПериодПланирования = 2 Тогда // ежемесячно
		
		НачалоМесяца = Дата(Год(РасчетнаяДатаНачала), Месяц(РасчетнаяДатаНачала), 1);
		КрайнийДеньМесяца = День(КонецМесяца(НачалоМесяца));
		
		Если КрайнийДеньМесяца < НомерДня Тогда
			СтартоваяДата = НачалоДня(КонецМесяца(НачалоМесяца));
		Иначе
			СтартоваяДата = Дата(Год(НачалоМесяца), Месяц(НачалоМесяца), НомерДня);
		КонецЕсли;
		
		Если СтартоваяДата < РасчетнаяДатаНачала Тогда
			НачалоМесяца = ДобавитьМесяц(НачалоМесяца, 1);
			ДатаГрафика = Дата(Год(НачалоМесяца), Месяц(НачалоМесяца), НомерДня);
		Иначе
			ДатаГрафика = СтартоваяДата;
		КонецЕсли;
		
		Пока ДатаГрафика <= РасчетнаяДатаОкончания Цикл
			
			НоваяСтрока = Движения.ГрафикПлановыхДоходовРасходов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭтотОбъект, "ВидДвижения,Статья,Сумма,Комментарий");
			НоваяСтрока.Период = ДатаГрафика;
			
			НачалоМесяца = ДобавитьМесяц(НачалоМесяца, 1);
			КрайнийДеньМесяца = День(КонецМесяца(НачалоМесяца));
			
			Если КрайнийДеньМесяца < НомерДня Тогда
				ДатаГрафика = НачалоДня(КонецМесяца(НачалоМесяца));
			Иначе
				ДатаГрафика = Дата(Год(НачалоМесяца), Месяц(НачалоМесяца), НомерДня);
			КонецЕсли;	
			
		КонецЦикла;	
		
	ИначеЕсли ПериодПланирования = 3 Тогда // ежегодно
		
		СтартоваяДата = Дата(Год(РасчетнаяДатаНачала), Месяц(ДатаПланирования), День(ДатаПланирования));
		
		Если СтартоваяДата < РасчетнаяДатаНачала Тогда
			ДатаГрафика = ДобавитьМесяц(СтартоваяДата, 12);
		Иначе
			ДатаГрафика = СтартоваяДата;
		КонецЕсли;
		
		Пока ДатаГрафика <= РасчетнаяДатаОкончания Цикл
			
			НоваяСтрока = Движения.ГрафикПлановыхДоходовРасходов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭтотОбъект, "ВидДвижения,Статья,Сумма,Комментарий");
			НоваяСтрока.Период = ДатаГрафика;
			ДатаГрафика = ДобавитьМесяц(ДатаГрафика, 12);
			
		КонецЦикла;	
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ПериодПланирования = 1 И НомерДня > 7 Тогда
		ОбщегоНазначения.СообщитьПользователю("Неправильно указан номер дня недели!");
		Отказ = Истина;
	ИначеЕсли ПериодПланирования = 2 И НомерДня > 31 Тогда
		ОбщегоНазначения.СообщитьПользователю("Неправильно указано число месяца!");
		Отказ = Истина;
	КонецЕсли;		
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли



