#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ДокументКомиссии) Тогда
	     ДокументКомиссииОбъект = ДокументКомиссии.ПолучитьОбъект();
		 ДокументКомиссииОбъект.Записать(РежимЗаписи, РежимПроведения);
	 КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ДвижениеРасход = Движения.ОстаткиПоСчетам.ДобавитьРасход();
	ЗаполнитьЗначенияСвойств(ДвижениеРасход, Ссылка);
	ДвижениеРасход.Сумма 	= Сумма;
	ДвижениеРасход.Период 	= Дата;
	
	ДвижениеПриход = Движения.ОстаткиПоСчетам.ДобавитьПриход();
	ДвижениеПриход.Период 	= Дата;
	ДвижениеПриход.Счет 	= СчетНазначения;
	ДвижениеПриход.Валюта 	= ВалютаНазначения;
	ДвижениеПриход.Сумма 	= СуммаНазначения;
		
	Если Валюта <> ВалютаНазначения Тогда
	
		РегистрыНакопления.ОстаткиВалют.ОтразитьСписаниеВалюты(ЭтотОбъект);
		РегистрыНакопления.ОстаткиВалют.ОтразитьПоступлениеВалюты(ЭтотОбъект);
	
	КонецЕсли;
	
	Если УчитыватьВРасходах Тогда
		
		ДвижениеРасходы = Движения.Расходы.Добавить();
		ДвижениеРасходы.Период 		= Дата;
		ЗаполнитьЗначенияСвойств(ДвижениеРасходы, Ссылка);
		
		Если Валюта <> ОбщегоНазначенияПовтИсп.ОсновнаяВалютаУчета() Тогда
			ДвижениеРасходы.СуммаУчетн = РегистрыНакопления.ОстаткиВалют.ПолучитьУчетнуюСуммуИзНабораЗаписей(Движения.ОстаткиВалют,
																						ВидДвиженияНакопления.Расход);	
		Иначе
			ДвижениеРасходы.СуммаУчетн = Сумма;
		КонецЕсли;																					
		
	КонецЕсли;	
	
	Если УчитыватьВДоходах Тогда
		
		ДвижениеДоходы = Движения.Доходы.Добавить();
		ДвижениеДоходы.Период 		= Дата;
		ДвижениеДоходы.Валюта 		= ВалютаНазначения;
		ДвижениеДоходы.СтатьяДДС 	= СтатьяДДС;
		ДвижениеДоходы.Сумма 		= СуммаНазначения;
		
		Если ВалютаНазначения <> ОбщегоНазначенияПовтИсп.ОсновнаяВалютаУчета() Тогда
			
			ДвижениеДоходы.СуммаУчетн 	= РегистрыНакопления.ОстаткиВалют.ПолучитьУчетнуюСуммуИзНабораЗаписей(Движения.ОстаткиВалют,
																						ВидДвиженияНакопления.Приход);
																						
		Иначе
			ДвижениеДоходы.СуммаУчетн = СуммаНазначения;
		КонецЕсли;
		
	КонецЕсли;
	
	// движения по регистру "Планируемые покупки"
	Если ЗначениеЗаполнено(ПланируемаяПокупка) Тогда
		
		ДвижениеПП = Движения.ОстаткиПланируемыхПокупок.ДобавитьРасход();
		ДвижениеПП.Период 	= Дата;
		ДвижениеПП.ПланируемаяПокупка = ПланируемаяПокупка;
		
		Если Валюта <> ОбщегоНазначенияПовтИсп.ОсновнаяВалютаУчета() Тогда
			ДвижениеПП.Сумма = РегистрыНакопления.ОстаткиВалют.ПолучитьУчетнуюСуммуИзНабораЗаписей(Движения.ОстаткиВалют,
																						ВидДвиженияНакопления.Расход);	
		Иначе
			ДвижениеПП.Сумма = Сумма;
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;	

	// если распроводим документ перемещения, то принудительно распроводим документ комиссии
	Если ЗначениеЗаполнено(ДокументКомиссии) И ДокументКомиссии.Проведен Тогда
		
		ДокументКомиссииОбъект = ДокументКомиссии.ПолучитьОбъект();
		ДокументКомиссииОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли



