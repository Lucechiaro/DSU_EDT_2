#Область ОписаниеПеременных

&НаКлиенте
Перем ДниНедели;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.ДокументКомиссии) Тогда
		НаличиеКомиссии = Объект.ДокументКомиссии.Проведен;
		ЗаполнитьРеквизитыКомисииИзДокументаРасхода();
	Иначе
		КурсКомиссии = 1;
		КратностьКомиссии = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьДоступностьРеквизитовКомиссии();
	
	ДниНедели = ОбщегоНазначенияКлиент.ПолучитьСоответствиеДнейНедели();
	ОбщегоНазначенияКлиент.ОбновитьНадписьДеньНедели(Элементы.ДеньНедели, ДниНедели, Объект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если (Объект.УчитыватьВДоходах Или Объект.УчитыватьВРасходах) И Не ЗначениеЗаполнено(Объект.СтатьяДДС) Тогда
		
		Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не заполнена статья движения!");
				
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	 // создадим документ
	Если НаличиеКомиссии Тогда
		
		// получим объект у существующей ссылки или создадим новый объект
		Если Не ЗначениеЗаполнено(ТекущийОбъект.ДокументКомиссии) Тогда
			ДокументКомиссии = Документы.РасходДенежныхСредств.СоздатьДокумент();
			ДокументКомиссии.Дата = ТекущийОбъект.Дата;
			ДокументКомиссии.СтатьяДДС = Справочники.СтатьиДДС.Комиссия;
			ДокументКомиссии.Записать();
			ТекущийОбъект.ДокументКомиссии = ДокументКомиссии.Ссылка;
		Иначе
			ДокументКомиссии = ТекущийОбъект.ДокументКомиссии.ПолучитьОбъект();
		КонецЕсли;	        
		
		// перепишем в него все данные из формы
		ЗаполнитьДокументРасходаИзРеквизитовКомиссии(ДокументКомиссии);
		                   
		// запишем/проведем документ
		ЗаписатьДокументКомиссии(ДокументКомиссии);

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаличиеКомиссииПриИзменении(Элемент)
	
	ПриИзменииФлагаНаличиеКомиссии();
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументКомиссииПриИзменении(Элемент)
	
	ПриИзмененииДокументаКомиссии();	
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиент.ОбновитьНадписьДеньНедели(Элементы.ДеньНедели, ДниНедели, Объект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РассчитатьКурс(Команда)
	
	РассчитатьКурсПеремещения();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриИзменииФлагаНаличиеКомиссии()
	
	ОбновитьДоступностьРеквизитовКомиссии();
	
	Если ЗначениеЗаполнено(Объект.ДокументКомиссии) Тогда
		ЗаписатьДокументКомиссии(Объект.ДокументКомиссии.ПолучитьОбъект());	
	Иначе
		
		СчетКомиссии = Объект.Счет;
		ВалютаКомиссии = Объект.Валюта;	
			
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДоступностьРеквизитовКомиссии()
	
	Элементы.ДанныеКомиссии.Доступность = НаличиеКомиссии;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьДокументКомиссии(ДокументКомиссии)
	
	Если ДокументКомиссии.ПометкаУдаления Тогда
		ДокументКомиссии.УстановитьПометкуУдаления(Ложь);
	КонецЕсли;
			
	ДокументКомиссии.Записать(?(НаличиеКомиссии И Объект.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.ОтменаПроведения), РежимПроведенияДокумента.Неоперативный);
		
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьРеквизитыКомисииИзДокументаРасхода()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	РасходДенежныхСредств.Курс Как КурсКомиссии,
	|	РасходДенежныхСредств.Кратность Как КратностьКомиссии,
	|	РасходДенежныхСредств.Счет Как СчетКомиссии,
	|	РасходДенежныхСредств.Валюта Как ВалютаКомиссии,
	|	РасходДенежныхСредств.Сумма Как СуммаКомиссии
	|ИЗ
	|	Документ.РасходДенежныхСредств КАК РасходДенежныхСредств
	|ГДЕ
	|	РасходДенежныхСредств.Ссылка = &Документ";
	Запрос.УстановитьПараметр("Документ", Объект.ДокументКомиссии);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДокументРасходаИзРеквизитовКомиссии(ДокументРасхода)
	
	ДокументРасхода.Курс 		= КурсКомиссии;
	ДокументРасхода.Кратность 	= КратностьКомиссии;
	ДокументРасхода.Валюта 		= ВалютаКомиссии;
	ДокументРасхода.Счет 		= СчетКомиссии;
	ДокументРасхода.Сумма 		= СуммаКомиссии;
	
КонецПроцедуры	

&НаСервере
Процедура ПриИзмененииДокументаКомиссии()
	
	НаличиеКомиссии = ЗначениеЗаполнено(Объект.ДокументКомиссии);   
	ОбновитьДоступностьРеквизитовКомиссии();
	ЗаполнитьРеквизитыКомисииИзДокументаРасхода();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьКурсПеремещения()
	
	СуммыЗаполнены = ЗначениеЗаполнено(Объект.Сумма) И ЗначениеЗаполнено(Объект.СуммаНазначения);
	
	Если СуммыЗаполнены Тогда
		
		РасчитанныйКурс = Объект.Сумма / Объект.СуммаНазначения;
		
		Если РасчитанныйКурс < 1 Тогда
			
			РасчитаннаяКратность = 1;
			ШагКратности = 10;
			
			Пока РасчитанныйКурс < 1 Цикл
				
				РасчитаннаяКратность = РасчитаннаяКратность * ШагКратности;
				РасчитанныйКурс = РасчитанныйКурс * ШагКратности;
			
			КонецЦикла;	
			
			Объект.Курс = РасчитанныйКурс;
			Объект.Кратность = РасчитаннаяКратность;
				
		Иначе
			
			Объект.Курс = РасчитанныйКурс;
			Объект.Кратность = 1;
						
		КонецЕсли;	
		
	Иначе
		
		Объект.Курс = 1;
		Объект.Кратность = 1;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
