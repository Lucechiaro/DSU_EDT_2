#Область ПрограммныйИнтерфейс

Функция ПолучитьОсновнуюВалютуУчета() Экспорт 
	
	Возврат ОбщегоНазначенияПовтИсп.ОсновнаяВалютаУчета();
	
КонецФункции

Функция ПолучитьСуммуЛиквидов(ДатаОтчета) Экспорт 
	
	Результат = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СУММА(ОстаткиПоСчетам.СуммаОстаток * КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность) КАК Сумма
	               |ИЗ
	               |	РегистрНакопления.ОстаткиПоСчетам.Остатки(
	               |			&ДатаОтчета,
	               |			Счет В
	               |				(ВЫБРАТЬ
	               |					Справочник.Счета.Ссылка
	               |				ИЗ
	               |					Справочник.Счета
	               |				ГДЕ
	               |					Справочник.Счета.Забалансовый = ЛОЖЬ
	               |					И Справочник.Счета.Ликвидный = ИСТИНА)) КАК ОстаткиПоСчетам
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОтчета, ) КАК КурсыВалютСрезПоследних
	               |		ПО ОстаткиПоСчетам.Валюта = КурсыВалютСрезПоследних.Валюта";
	Запрос.УстановитьПараметр("ДатаОтчета", Новый Граница(ДатаОтчета, ВидГраницы.Включая));				   
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Сумма;
	КонецЕсли;
	
	Возврат Результат
	
КонецФункции	

Функция ПолучитьСтруктуруРасшифровки(Расшифровка, ДанныеРасшифровки) Экспорт 
	
	СтруктураРасшифровки = Новый Структура;
	Коллекция = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	Элемент = Коллекция.Элементы[Расшифровка];
	ОбработатьПолеРасшифровки(Элемент, СтруктураРасшифровки);
	
	Возврат СтруктураРасшифровки;
	
КонецФункции

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура ОбработатьПолеРасшифровки(Элемент, СтруктураРасшифровки) 

	Если ТипЗнч(Элемент) = Тип("ЭлементРасшифровкиКомпоновкиДанныхПоля") Тогда 
		
		ПоляЭлемента = Элемент.ПолучитьПоля();	
		
		Для Каждого ПолеЭлемента Из ПоляЭлемента Цикл
			
			Попытка
				СтруктураРасшифровки.Вставить(СтрЗаменить(ПолеЭлемента.Поле, ".", ""), ПолеЭлемента.Значение);
			Исключение
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Ошибка получения расшифровки");
			КонецПопытки;
			
		КонецЦикла;	
		
	КонецЕсли;
	
	РодителиПоля = Элемент.ПолучитьРодителей();
	
	Для Каждого РодительПоля Из РодителиПоля Цикл
		ОбработатьПолеРасшифровки(РодительПоля, СтруктураРасшифровки)
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти
	