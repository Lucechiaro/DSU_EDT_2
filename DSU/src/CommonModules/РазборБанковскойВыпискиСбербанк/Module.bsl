#Область СлужебныйПрограммныйИнтерфейс

Процедура РазобратьВыписку(ТаблицаТранзакций, ИмяФайла) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент();
	ТабличныйДокумент.Прочитать(ИмяФайла, СпособЧтенияЗначенийТабличногоДокумента.Значение);
	
	ТаблицаТранзакций.Очистить();
	КоличествоКолонок = ТабличныйДокумент.ШиринаТаблицы;
	
	Для НомерСтроки = 2 По ТабличныйДокумент.ВысотаТаблицы Цикл
		
		МассивПоказателей = РазобратьСтрокуВыписки(ТабличныйДокумент, НомерСтроки, КоличествоКолонок);
		ДобавитьСтрокуТранзакции(ТаблицаТранзакций, МассивПоказателей);
		
	КонецЦикла;
		
КонецПроцедуры	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РазобратьСтрокуВыписки(ТабличныйДокумент, НомерСтроки, КоличествоКолонок)
	
	МассивПоказателей = Новый Массив;
	
	Для НомерКолонки = 1 По КоличествоКолонок Цикл
		
		МассивПоказателей.Добавить(ТабличныйДокумент.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки).Текст)
		
	КонецЦикла;	
	
	Возврат МассивПоказателей;
	
КонецФункции

Процедура ДобавитьСтрокуТранзакции(ТаблицаТранзакций, МассивПоказателей)
	
	СтатусТранзакции = МассивПоказателей[8];
	ТранзакцияАктивна = СтрСравнить(СтатусТранзакции, "Активная") = 0;
	
	Если Не ТранзакцияАктивна Тогда
		Возврат;
	КонецЕсли;	
		
	Сумма = Число(СтрЗаменить(МассивПоказателей[4], """", ""));
	СуммаВВалютеСчета = Число(СтрЗаменить(МассивПоказателей[6], """", ""));
	ЭтоРасход = СтрСравнить(МассивПоказателей[2], "Расходы") = 0;
	
	Если ЭтоРасход Тогда	
		ВидДвижения = ПредопределенноеЗначение("Перечисление.ВидДвиженияДС.Расход");
	Иначе
		ВидДвижения = ПредопределенноеЗначение("Перечисление.ВидДвиженияДС.Доход");	
	КонецЕсли;
	
	Дата = ДатаИзСтроки(МассивПоказателей[1]);
		
	СтрокаТранзакции = ТаблицаТранзакций.Добавить();
	СтрокаТранзакции.ВидДвижения 		= ВидДвижения;
	СтрокаТранзакции.ДатаОперации 		= НачалоДня(Дата);
	СтрокаТранзакции.ДатаПоБанку 		= Дата;
	СтрокаТранзакции.Сумма 				= ОбщегоНазначенияКлиентСервер.Модуль(Сумма);
	СтрокаТранзакции.СуммаВВалютеСчета 	= ОбщегоНазначенияКлиентСервер.Модуль(СуммаВВалютеСчета);
	СтрокаТранзакции.Содержание 		= МассивПоказателей[7];
	СтрокаТранзакции.КраткоеСодержание	= МассивПоказателей[3];
	СтрокаТранзакции.ИдентификаторКорреспондента = "";
	СтрокаТранзакции.КодВалюты 			= МассивПоказателей[5];
	СтрокаТранзакции.ИдентификаторТранзакции = СтрокаТранзакции.КодВалюты + Строка(СтрокаТранзакции.ДатаПоБанку) 
											+ Строка(СтрокаТранзакции.Сумма);
									
КонецПроцедуры

Функция ДатаИзСтроки(Знач ДатаСтрокой)
	
	Попытка
		
		// пример 21 мая 2025, 09:15
		ЧастиДатыСтрокой = СтрРазделить(ДатаСтрокой, ",");
		ДеньСтрокой = ЧастиДатыСтрокой[0];
		ВремяСтрокой = ЧастиДатыСтрокой[1];
		
		ЧастиДня = СтрРазделить(ДеньСтрокой, " ");
		НомерДня = Число(ЧастиДня[0]);
		НомерМесяца = НомерМесяцаПоПредставлению(ЧастиДня[1]);
		НомерГода = Число(ЧастиДня[2]);
		
		ЧастиВремени = СтрРазделить(ВремяСтрокой, ":");
		НомерЧаса = Число(ЧастиВремени[0]);
		НомерМинуты = Число(ЧастиВремени[1]);
		
		ДатаРезультат = Дата(НомерГода, НомерМесяца, НомерДня, НомерЧаса, НомерМинуты, 0);
		
	Исключение
		
		ВызватьИсключение СтрШаблон("Не удалось разобрать дату с представлением '%1'", ДатаСтрокой);
					
	КонецПопытки;		
	
	Возврат ДатаРезультат; 
	
КонецФункции

Функция НомерМесяцаПоПредставлению(ПредставлениеДня)
	
	НомераДней = Новый Соответствие();
	НомераДней.Вставить("янв.", 1);
	НомераДней.Вставить("фев.", 2);
	НомераДней.Вставить("мар.", 3);
	НомераДней.Вставить("апр.", 4);
	НомераДней.Вставить("мая", 5);
	НомераДней.Вставить("июн.", 6);
	НомераДней.Вставить("июл.", 7);
	НомераДней.Вставить("авг.", 8);
	НомераДней.Вставить("сент.", 9);
	НомераДней.Вставить("окт.", 10);
	НомераДней.Вставить("нояб.", 11);
	НомераДней.Вставить("дек.", 12);
	
	Возврат НомераДней[ПредставлениеДня];
	
КонецФункции	

#КонецОбласти
