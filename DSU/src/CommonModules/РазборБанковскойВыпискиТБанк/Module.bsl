#Область СлужебныйПрограммныйИнтерфейс

Процедура РазобратьВыписку(ТаблицаТранзакций, ИмяФайла) Экспорт
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ИмяФайла, КодировкаТекста.UTF8);
	
	ТаблицаТранзакций.Очистить();
	
	Для НомерСтроки = 2 По ТекстовыйДокумент.КоличествоСтрок() Цикл
		
		МассивПоказателей = РазобратьСтрокуВыписки(ТекстовыйДокумент.ПолучитьСтроку(НомерСтроки));
		ДобавитьСтрокуТранзакции(ТаблицаТранзакций, МассивПоказателей);
		
	КонецЦикла;	
	
КонецПроцедуры	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РазобратьСтрокуВыписки(СтрокаВыписки)
	
	МассивПоказателей = Новый Массив;
	ПредыдущийИндекс = 0; 
	Разделитель = ";";
		
	Пока Истина Цикл
		
		ТекущийИндекс = СтрНайти(СтрокаВыписки, Разделитель, , ПредыдущийИндекс + 1);

		Если ТекущийИндекс = 0 Тогда

			ЗначениеСтрокой = Сред(СтрокаВыписки, ПредыдущийИндекс + 1, СтрДлина(СтрокаВыписки) - ПредыдущийИндекс - 1);
			МассивПоказателей.Добавить(ЗначениеСтрокой);
			Прервать;

		Иначе

			ЗначениеСтрокой = Сред(СтрокаВыписки, ПредыдущийИндекс + 1, ТекущийИндекс - ПредыдущийИндекс - 1);
			МассивПоказателей.Добавить(ЗначениеСтрокой);
			ПредыдущийИндекс = ТекущийИндекс;

		КонецЕсли;	   
		
		Если ТекущийИндекс >= СтрДлина(СтрокаВыписки) Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;  
	
	УбратьКавычкиИзСтрокПоказателей(МассивПоказателей);
	
	Возврат МассивПоказателей;
	
КонецФункции

Процедура ДобавитьСтрокуТранзакции(ТаблицаТранзакций, МассивПоказателей)
	
	СтатусТранзакции = МассивПоказателей[3];
	
	Если СтрСравнить(СтатусТранзакции, "OK") <> 0 Тогда
		Возврат;
	КонецЕсли;	
		
	Сумма = Число(СтрЗаменить(МассивПоказателей[4], """", ""));
	СуммаВВалютеСчета = Сумма;
	
	Если Сумма = 0 Тогда
		Возврат;
	ИначеЕсли Сумма > 0 Тогда	
		ВидДвижения = ПредопределенноеЗначение("Перечисление.ВидДвиженияДС.Доход");
	Иначе
		ВидДвижения = ПредопределенноеЗначение("Перечисление.ВидДвиженияДС.Расход");	
	КонецЕсли;
	
	Дата = ДатаИзСтроки(МассивПоказателей[0]);
		
	СтрокаТранзакции = ТаблицаТранзакций.Добавить();
	СтрокаТранзакции.ВидДвижения 		= ВидДвижения;
	СтрокаТранзакции.ДатаОперации 		= НачалоДня(Дата);
	СтрокаТранзакции.ДатаПоБанку 		= Дата;
	СтрокаТранзакции.Сумма 				= ОбщегоНазначенияКлиентСервер.Модуль(Сумма);
	СтрокаТранзакции.СуммаВВалютеСчета 	= ОбщегоНазначенияКлиентСервер.Модуль(СуммаВВалютеСчета);
	СтрокаТранзакции.Содержание 		= МассивПоказателей[11];
	СтрокаТранзакции.КраткоеСодержание	= МассивПоказателей[9];
	СтрокаТранзакции.ИдентификаторКорреспондента = МассивПоказателей[10];
	СтрокаТранзакции.КодВалюты 			= МассивПоказателей[7];
	СтрокаТранзакции.ИдентификаторТранзакции = СтрокаТранзакции.КодВалюты + Строка(СтрокаТранзакции.ДатаПоБанку) 
											+ Строка(СтрокаТранзакции.Сумма);
									
КонецПроцедуры

Процедура УбратьКавычкиИзСтрокПоказателей(МассивПоказателей)
	
	Для Индекс = 0 По МассивПоказателей.Количество() - 1 Цикл
		СтрокаПоказателя = МассивПоказателей[Индекс];
		МассивПоказателей[Индекс] = УбратьКавычкиИзСтрокиПоказателя(СтрокаПоказателя);	
	КонецЦикла;	
	
КонецПроцедуры

Функция УбратьКавычкиИзСтрокиПоказателя(СтрокаПоказателя)
	
	Кавычки = """";
	
	Если СтрСравнить(Лев(СтрокаПоказателя, 1), Кавычки) = 0 Тогда
		СтрокаПоказателя = Прав(СтрокаПоказателя, СтрДлина(СтрокаПоказателя) - 1);
	КонецЕсли;  
	
	Если СтрСравнить(Прав(СтрокаПоказателя, 1), Кавычки) = 0 Тогда
		СтрокаПоказателя = Лев(СтрокаПоказателя, СтрДлина(СтрокаПоказателя) - 1);
	КонецЕсли; 
	
	Возврат СтрокаПоказателя;
	
КонецФункции

Функция ДатаИзСтроки(Знач ДатаСтрокой)
	
	Дата = Дата(1,1,1);
	
	Попытка
		
		Если СтрНайти(ДатаСтрокой, ":") > 0 Тогда
			Дата = Дата(ДатаСтрокой); 
		Иначе
			
			// предполагаем формат "dd.mm.yyyy"
			СоставныеЧастиДаты = СтрРазделить(ДатаСтрокой, ".");
			
			Если СоставныеЧастиДаты.Количество() = 3 Тогда
				Дата = Дата(Число(СоставныеЧастиДаты[2]), Число(СоставныеЧастиДаты[1]), Число(СоставныеЧастиДаты[0]));				
			КонецЕсли;	 
			
		КонецЕсли;		
		
	Исключение
		
		ТекстИсключения = СтрШаблон("Не удалось разобрать дату вида '%1'", ДатаСтрокой);
		ВызватьИсключение ТекстИсключения;
			
	КонецПопытки;
	
	Возврат Дата;
		
КонецФункции

#КонецОбласти
