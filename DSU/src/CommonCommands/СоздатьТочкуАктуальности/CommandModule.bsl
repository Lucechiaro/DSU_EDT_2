#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ОООкончаниеВыбораДаты = Новый ОписаниеОповещения("ОООкончаниеВыбораДаты", ЭтотОбъект);
	
	ПараметрыВыбораДаты = Новый Структура;
	ПараметрыВыбораДаты.Вставить("Заголовок", "Введите дату актуальности");
	ПараметрыВыбораДаты.Вставить("Дата", 		ТекущаяДата());
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораДаты", ПараметрыВыбораДаты, , , , , ОООкончаниеВыбораДаты, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОООкончаниеВыбораДаты(ДатаСреза, ДополнительныеПараметры)  Экспорт
	
	Если ЗначениеЗаполнено(ДатаСреза) Тогда
		
		Если Не КурсыВалютАктуальны(ДатаСреза) Тогда
			
			ПараметрыВызова = Новый Структура("ДатаСреза", ДатаСреза);
			ОООтветАктуализация = Новый ОписаниеОповещения("ОООтветАктуализация", ЭтотОбъект, ПараметрыВызова);
			ПоказатьВопрос(ОООтветАктуализация, "Курсы валют неактуальны! Вы хотите обновить курсы валют?", РежимДиалогаВопрос.ДаНетОтмена);
			
		Иначе
			
			СоздатьТочкуАктуальности(ДатаСреза);	
			
		КонецЕсли;
			
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ОООтветАктуализация(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		СоздатьТочкуАктуальности(ДополнительныеПараметры.ДатаСреза);
	ИначеЕсли Ответ = КодВозвратаДиалога.Да Тогда 
		
		ПараметрыВводаКурсов = Новый Структура;
		ПараметрыВводаКурсов.Вставить("Дата", ДополнительныеПараметры.ДатаСреза);
		ОООкончаниеВводаКурсов = Новый ОписаниеОповещения("ОООкончаниеВводаКурсов", ЭтотОбъект, ПараметрыВводаКурсов);
		ОткрытьФорму("ОбщаяФорма.ФормаВводаКурсовВалют", ПараметрыВводаКурсов, , , , , ОООкончаниеВводаКурсов, );
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОООкончаниеВводаКурсов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		СоздатьТочкуАктуальности(ДополнительныеПараметры.Дата);	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьТочкуАктуальности(Дата)
	
	СоздатьТочкуАктуальностиНаСервере(Дата);
	Оповестить("ОбновленыКурсыВалют");
	
КонецПроцедуры

&НаСервере
Процедура СоздатьТочкуАктуальностиНаСервере(Дата)
	
	// получаем остатки на конец текущего дня
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДенежныеСредстваОстатки.Счет КАК Счет,
	               |	&Дата КАК Период,
	               |	ДенежныеСредстваОстатки.Валюта КАК Валюта,
	               |	ДенежныеСредстваОстатки.СуммаОстаток КАК Сумма,
	               |	ДенежныеСредстваОстатки.СуммаОстаток * КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность КАК СуммаУчетн
	               |ИЗ
	               |	РегистрНакопления.ОстаткиПоСчетам.Остатки(&ДатаСреза, Не Счет.Забалансовый) КАК ДенежныеСредстваОстатки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаСреза, ) КАК КурсыВалютСрезПоследних
	               |		ПО ДенежныеСредстваОстатки.Валюта = КурсыВалютСрезПоследних.Валюта";
	Запрос.УстановитьПараметр("ДатаСреза", 	Новый Граница(КонецДня(Дата), ВидГраницы.Включая));	
	Запрос.УстановитьПараметр("Дата", 		Дата);		   
	ТаблицаДляЗаписи = Запрос.Выполнить().Выгрузить();
	
	// записываем таблицу остатков в регистр
	НаборЗаписей = РегистрыСведений.ТочкиАктуальности.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Период.Установить(Дата);
	НаборЗаписей.Загрузить(ТаблицаДляЗаписи);
	НаборЗаписей.Записать();
	
	// сообщим пользователю о новой точке актуальности
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Создана точка актуальности " + Формат(Дата, "ДЛФ=DD");
	Сообщение.Сообщить();
	
КонецПроцедуры	

&НаСервере
Функция КурсыВалютАктуальны(ДатаСреза)
	
	Возврат РегистрыСведений.КурсыВалют.КурсыВалютАктуальны(ДатаСреза);
	
КонецФункции	

#КонецОбласти

