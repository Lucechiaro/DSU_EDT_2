#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция КурсВалюты(Валюта, Знач Период = Неопределено) Экспорт
	
	Если Период = Неопределено Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;	
	
	ПараметрыКурса = Новый Структура("Курс,Кратность,КурсККратности", 1, 1, 1);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Курс,
	|	КурсыВалютСрезПоследних.Кратность
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&ДатаКурсов, Валюта = &Валюта) КАК КурсыВалютСрезПоследних";
	Запрос.УстановитьПараметр("ДатаКурсов", 	Период);
	Запрос.УстановитьПараметр("Валюта", 		Валюта);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ЗаполнитьЗначенияСвойств(ПараметрыКурса, Выборка);
		ПараметрыКурса.КурсККратности = Выборка.Курс / Выборка.Кратность;
		
	КонецЕсли;		

	Возврат ПараметрыКурса;
	
КонецФункции

Функция ПолучитьТаблицуАктуальностиКурсов(ДатаКурсов, ОтображатьТолькоНеактуальныеКурсы = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Период,
	|	КурсыВалютСрезПоследних.Валюта
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(
	|			&ДатаКурсов, Валюта В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Остатки.Валюта
	|				ИЗ
	|					РегистрНакопления.ОстаткиПоСчетам.Остатки(&ДатаКурсов, Валюта.Используется И Валюта <> &ВалютаУчета) КАК Остатки)
	|			) КАК КурсыВалютСрезПоследних"
	+ ?(ОтображатьТолькоНеактуальныеКурсы, " Где КурсыВалютСрезПоследних.Период < НачалоПериода(&ДатаКурсов, День)", "");
	Запрос.УстановитьПараметр("ДатаКурсов", КонецДня(ДатаКурсов));
	Запрос.УстановитьПараметр("ВалютаУчета", Константы.ОсновнаяВалютаУчета.Получить());
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции	                                             

Функция КурсыВалютАктуальны(ДатаКурсов) Экспорт 
	
	ТаблицаКурсов = ПолучитьТаблицуАктуальностиКурсов(ДатаКурсов, Истина);
	Возврат ТаблицаКурсов.Количество() = 0;
	
КонецФункции

#КонецОбласти	

#КонецЕсли