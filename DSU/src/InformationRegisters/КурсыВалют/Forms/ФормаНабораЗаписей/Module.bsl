#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ОсновнаяВалюта = Константы.ОсновнаяВалютаУчета.Получить();
	ТаблицаКурсов.Параметры.УстановитьЗначениеПараметра("ОсновнаяВалюта", ОсновнаяВалюта);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьНадписьКурсыАктуальны();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновленыКурсыВалют" Тогда
		ОбновитьНадписьКурсыАктуальны();
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НадписьКурсыВалютНажатие(Элемент)
	
	ООЗакрытиеФормыВводаКурсов = Новый ОписаниеОповещения("ЗакрытиеФормыВводаКурсов", ЭтотОбъект);
	ОткрытьФорму("РегистрСведений.КурсыВалют.Форма.ВводКурсов", , ЭтотОбъект, , , ,
				ООЗакрытиеФормыВводаКурсов , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ОбновитьНадписьКурсыАктуальны();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыКурсы

&НаКлиенте
Процедура КурсыПриИзменении(Элемент)
	
	ОбновитьНадписьКурсыАктуальны();	
	
КонецПроцедуры

&НаКлиенте
Процедура КурсыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОбновитьНадписьКурсыАктуальны();
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьНадписьКурсыАктуальны()

	ЗаголовокКурсыВалют = Элементы.НадписьКурсыВалют;

	Если КурсыВалютАктуальны() Тогда
		
		ЗаголовокКурсыВалют.Заголовок = "Курсы валют актуальны";
		ЗаголовокКурсыВалют.ЦветТекста = WebЦвета.Зеленый;
		
	Иначе
		
		ЗаголовокКурсыВалют.Заголовок = "Курсы валют не актуальны. Нажмите, чтобы ввести курсы.";
		ЗаголовокКурсыВалют.ЦветТекста = WebЦвета.Красный;
		
	КонецЕсли;

КонецПроцедуры	

&НаКлиенте
Процедура ЗакрытиеФормыВводаКурсов(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.Курсы.Обновить();
	ОбновитьНадписьКурсыАктуальны();
	
КонецПроцедуры	

&НаСервереБезКонтекста
Функция КурсыВалютАктуальны(Знач ДатаКурсов = Неопределено) 
	
	Если ДатаКурсов = Неопределено Тогда
		ДатаКурсов = ТекущаяДатаСеанса();
	КонецЕсли;	
	
	Возврат РегистрыСведений.КурсыВалют.КурсыВалютАктуальны(ДатаКурсов);
	
КонецФункции	

#КонецОбласти
