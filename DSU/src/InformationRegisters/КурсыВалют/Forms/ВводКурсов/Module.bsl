#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("Дата") Тогда
		ДатаКурсов = Параметры.Дата;
	Иначе	
		ДатаКурсов = ТекущаяДатаСеанса();
	КонецЕсли;
		
	ПредыдущаяДатаКурсов = ДатаКурсов;
	ОтрисоватьРеквизитыФормы();

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)

	Если Модифицированность Тогда

		Ответ = ВопросАсинх("Данные были изменены. Сохранить изменения?", РежимДиалогаВопрос.ДаНетОтмена);

		Если Ответ = КодВозвратаДиалога.Да Тогда
			ЗаписатьНаСервере();
		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			Отказ = Истина;
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Асинх Процедура ДатаКурсовПриИзменении(Элемент)

	Если Модифицированность Тогда

		Обещание = ВопросАсинх("Данные были изменены. Сохранить изменения?", РежимДиалогаВопрос.ДаНетОтмена);
		Ответ = Ждать Обещание;

		Если Ответ = КодВозвратаДиалога.Да Тогда
			ВременнаяДата = ДатаКурсов;
			ДатаКурсов = ПредыдущаяДатаКурсов;
			ЗаписатьНаСервере();
			ДатаКурсов = ВременнаяДата;
			ПеречитатьКурсыВалют();
		ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
			ПеречитатьКурсыВалют();
			Модифицированность = Ложь;
		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			ДатаКурсов = ПредыдущаяДатаКурсов;
		КонецЕсли;

	Иначе

		ПеречитатьКурсыВалют();

	КонецЕсли;

	ПредыдущаяДатаКурсов = ДатаКурсов;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьКурсы(Команда)

	ЗаписатьНаСервере();
	Закрыть(Истина);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура КурсПриИзменении(Элемент)

	Модифицированность = Истина;
	ОбновитьОтображениеПредыдущегоКурса(Элемент.Имя);

КонецПроцедуры

&НаКлиенте
Процедура КратностьПриИзменении(Элемент)

	Модифицированность = Истина;
	ОбновитьОтображениеПредыдущегоКурса(Элемент.Имя);

КонецПроцедуры

&НаСервере
Функция ТипОписаниеПредыдущегоКурса()
	
	 Возврат Новый ОписаниеТипов("Строка");
	
КонецФункции	

// ОтрисоватьРеквизитыФормы - рисует данные курсов валют в виде реквизитов формы
&НаСервере
Процедура ОтрисоватьРеквизитыФормы()

	ОТКурсВалюты = Метаданные.РегистрыСведений.КурсыВалют.Ресурсы.Курс.Тип;
	ОТКратностьВалюты = Метаданные.РегистрыСведений.КурсыВалют.Ресурсы.Кратность.Тип;
	ОТОписаниеПредыдущегоКурса = ТипОписаниеПредыдущегоКурса();

	Массив = Новый Массив;

	ТаблицаВалют = ПолучитьТаблицуВалют(ДатаКурсов);
	Итератор = 1;

	Для Каждого СтрокаВалюты Из ТаблицаВалют Цикл

		НоваяСтрокаРеквизита = ТаблицаКурсов.Добавить();
		НоваяСтрокаРеквизита.ИмяРеквизита = "Валюта" + Строка(Итератор);
		ЗаполнитьЗначенияСвойств(НоваяСтрокаРеквизита, СтрокаВалюты);
		ОбновитьСостояниеПредыдущегоКурса(НоваяСтрокаРеквизита);

		РеквизитКурс = Новый РеквизитФормы("Курс_" + НоваяСтрокаРеквизита.ИмяРеквизита, ОТКурсВалюты, , "Курс");
		РеквизитКратность = Новый РеквизитФормы("Кратность_" + НоваяСтрокаРеквизита.ИмяРеквизита, ОТКратностьВалюты, ,
			"Кратность");
		РеквизитПрошлыйКурс = Новый РеквизитФормы("СостояниеПредыдущегоКурса_" + НоваяСтрокаРеквизита.ИмяРеквизита, ОТОписаниеПредыдущегоКурса);	
		Массив.Добавить(РеквизитКурс);
		Массив.Добавить(РеквизитКратность);
		Массив.Добавить(РеквизитПрошлыйКурс);

		Итератор = Итератор + 1;

	КонецЦикла;

	ИзменитьРеквизиты(Массив);
	
	// создадим элементы формы для отображения реквизитов
	Итератор = 1;

	Для Каждого СтрокаВалюты Из ТаблицаКурсов Цикл
		
		// создадим группу валюты
		ГруппаВалюты = Элементы.Добавить("Группа_" + СтрокаВалюты.ИмяРеквизита, Тип("ГруппаФормы"),
			Элементы.ГруппаКурсы);
		ГруппаВалюты.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаВалюты.Отображение = ОтображениеОбычнойГруппы.Нет;
		ГруппаВалюты.ОтображатьЗаголовок = Ложь;
		ГруппаВалюты.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		
		// выведем название валюты
		НазваниеВалюты = Элементы.Добавить("Название_" + СтрокаВалюты.ИмяРеквизита, Тип("ДекорацияФормы"), ГруппаВалюты);
		НазваниеВалюты.Вид = ВидДекорацииФормы.Надпись;
		НазваниеВалюты.Заголовок = СтрокаВалюты.Наименование + " (" + СтрокаВалюты.ПредставлениеВалюты + ")";
		НазваниеВалюты.Ширина = 17;
		
		// добавим поле курса
		ПолеКурса = Элементы.Добавить("Курс_" + СтрокаВалюты.ИмяРеквизита, Тип("ПолеФормы"), ГруппаВалюты);
		ПолеКурса.ПутьКДанным = "Курс_" + СтрокаВалюты.ИмяРеквизита;
		ПолеКурса.Вид = ВидПоляФормы.ПолеВвода;
		ПолеКурса.Заголовок = "Курс";
		ПолеКурса.Ширина = 5;
		ПолеКурса.УстановитьДействие("ПриИзменении", "КурсПриИзменении");
		ЭтотОбъект["Курс_" + СтрокаВалюты.ИмяРеквизита] = СтрокаВалюты.Курс;
		
		// добавим поле кратности
		ПолеКратности = Элементы.Добавить("Кратность_" + СтрокаВалюты.ИмяРеквизита, Тип("ПолеФормы"), ГруппаВалюты);
		ПолеКратности.ПутьКДанным = "Кратность_" + СтрокаВалюты.ИмяРеквизита;
		ПолеКратности.Вид = ВидПоляФормы.ПолеВвода;
		ПолеКратности.Заголовок = "Кратность";
		ПолеКратности.Ширина = 3;
		ПолеКратности.УстановитьДействие("ПриИзменении", "КратностьПриИзменении");
		ЭтотОбъект["Кратность_" + СтрокаВалюты.ИмяРеквизита] = СтрокаВалюты.Кратность;
		
		// информация о прошлом курсе
		ПолеПрошлыйКурс = Элементы.Добавить("СостояниеПредыдущегоКурса_" + СтрокаВалюты.ИмяРеквизита, Тип("ПолеФормы"), 
											ГруппаВалюты);
		ПолеПрошлыйКурс.ПутьКДанным = "СостояниеПредыдущегоКурса_" + СтрокаВалюты.ИмяРеквизита;
		ПолеПрошлыйКурс.Вид = ВидПоляФормы.ПолеНадписи;
		ПолеПрошлыйКурс.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
		ПолеПрошлыйКурс.Заголовок = "Предыдущий курс";
		ПолеПрошлыйКурс.Ширина = 20;
		ЭтотОбъект["СостояниеПредыдущегоКурса_" + СтрокаВалюты.ИмяРеквизита] = СтрокаВалюты.СостояниеПредыдущегоКурса;

		Итератор = Итератор + 1;

	КонецЦикла;

КонецПроцедуры

// ЗаписатьНаСервере - записывает данные о валютах в регистр сведений "Курсы валют"
&НаСервере
Процедура ЗаписатьНаСервере()

	НаборЗаписей = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Период.Установить(ДатаКурсов);
	НаборЗаписей.Прочитать();

	ТаблицаРегистра = НаборЗаписей.Выгрузить();
	ТаблицаРегистра.Очистить();

	Для Каждого СтрокаВалюты Из ТаблицаКурсов Цикл
		
		НоваяСтрока = ТаблицаРегистра.Добавить();
		НоваяСтрока.Период = ДатаКурсов;
		НоваяСтрока.Валюта = СтрокаВалюты.Валюта;
		НоваяСтрока.Курс = ЭтотОбъект["Курс_" + СтрокаВалюты.ИмяРеквизита];
		НоваяСтрока.Кратность = ЭтотОбъект["Кратность_" + СтрокаВалюты.ИмяРеквизита];
		
	КонецЦикла;

	НаборЗаписей.Загрузить(ТаблицаРегистра);
	НаборЗаписей.Записать();

	Модифицированность = Ложь;

КонецПроцедуры	

// Получить таблицу валют.
// получает курсы валют на конкретную дату
// Параметры:
//  ДатаКурсов - Дата - Дата курсов
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Получить таблицу валют
&НаСервереБезКонтекста
Функция ПолучитьТаблицуВалют(ДатаКурсов)

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	КурсыВалют.Валюта КАК Валюта,
	|	КурсыВалют.Курс КАК Курс,
	|	КурсыВалют.Кратность КАК Кратность
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	РегистрСведений.КурсыВалют КАК КурсыВалют
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Валюты КАК Валюты
	|		ПО КурсыВалют.Валюта = Валюты.Ссылка
	|ГДЕ
	|	КурсыВалют.Период = &ДатаКурсов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалютИстория.Валюта КАК Валюта,
	|	КурсыВалютИстория.Период КАК Период
	|ПОМЕСТИТЬ КурсыВалютДоДаты
	|ИЗ
	|	РегистрСведений.КурсыВалют КАК КурсыВалютИстория
	|ГДЕ
	|	КурсыВалютИстория.Период < &ДатаКурсов
	|	И КурсыВалютИстория.Курс <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалютДоДаты.Валюта КАК Валюта,
	|	МАКСИМУМ(КурсыВалютДоДаты.Период) КАК Период
	|ПОМЕСТИТЬ ДатыПредыдущихКурсов
	|ИЗ
	|	КурсыВалютДоДаты КАК КурсыВалютДоДаты
	|СГРУППИРОВАТЬ ПО
	|	КурсыВалютДоДаты.Валюта
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыПредыдущихКурсов.Валюта КАК Валюта,
	|	ДатыПредыдущихКурсов.Период КАК Период,
	|	КурсыВалют.Курс КАК Курс,
	|	КурсыВалют.Кратность КАК Кратность
	|ПОМЕСТИТЬ ПредыдущиеКурсыВалют
	|ИЗ
	|	ДатыПредыдущихКурсов КАК ДатыПредыдущихКурсов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|		ПО ДатыПредыдущихКурсов.Валюта = КурсыВалют.Валюта
	|		И ДатыПредыдущихКурсов.Период = КурсыВалют.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Валюты.Ссылка КАК Валюта,
	|	Валюты.Наименование КАК Наименование,
	|	Валюты.ПредставлениеВалюты КАК ПредставлениеВалюты,
	|	ЕСТЬNULL(КурсыВалют.Курс, 0) КАК Курс,
	|	ЕСТЬNULL(КурсыВалют.Кратность, 1) КАК Кратность,
	|	ЕСТЬNULL(ПредыдущиеКурсыВалют.Курс, 0) КАК ПредыдущийКурс,
	|	ЕСТЬNULL(ПредыдущиеКурсыВалют.Кратность, 1) КАК ПредыдущаяКратность,
	|	ЕСТЬNULL(ПредыдущиеКурсыВалют.Период, 1) КАК ДатаПредыдущегоКурса
	|ИЗ
	|	Справочник.Валюты КАК Валюты
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	|		ПО Валюты.Ссылка = КурсыВалют.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПредыдущиеКурсыВалют КАК ПредыдущиеКурсыВалют
	|		ПО Валюты.Ссылка = ПредыдущиеКурсыВалют.Валюта
	|ГДЕ
	|	Валюты.Используется
	|	И Валюты.Ссылка <> &ОсновнаяВалюта";
	Запрос.УстановитьПараметр("ДатаКурсов", ДатаКурсов);
	Запрос.УстановитьПараметр("ОсновнаяВалюта", ОбщегоНазначенияПовтИсп.ОсновнаяВалютаУчета());

	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции	

// Перечитать курсы валют.
// обновляет данные курсов валют на форме
&НаСервере
Процедура ПеречитатьКурсыВалют()

	Отбор = Новый Структура;
	ТаблицаВалют = ПолучитьТаблицуВалют(ДатаКурсов);

	Для Каждого СтрокаВалюты Из ТаблицаВалют Цикл

		Отбор.Вставить("Валюта", СтрокаВалюты.Валюта);
		МассивСтрок = ТаблицаКурсов.НайтиСтроки(Отбор);

		Если МассивСтрок.Количество() > 0 Тогда
			
			ТекущаяСтрока = МассивСтрок[0];
			ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтрокаВалюты);
			ЭтотОбъект["Курс_" + ТекущаяСтрока.ИмяРеквизита] = СтрокаВалюты.Курс;
			ЭтотОбъект["Кратность_" + ТекущаяСтрока.ИмяРеквизита] = СтрокаВалюты.Кратность;
			ОбновитьСостояниеПредыдущегоКурса(ТекущаяСтрока);
			ЭтотОбъект["СостояниеПредыдущегоКурса_" + ТекущаяСтрока.ИмяРеквизита] = ТекущаяСтрока.СостояниеПредыдущегоКурса;
			
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьСостояниеПредыдущегоКурса(СтрокаКурса)
	
	ДатаПредыдущегоКурса = Формат(СтрокаКурса.ДатаПредыдущегоКурса, "ДФ=dd.MM.yyyy;");
	ПрошлыйКурс = Формат(СтрокаКурса.ПредыдущийКурс/СтрокаКурса.ПредыдущаяКратность, "ЧДЦ=2;");
	
	Если СтрокаКурса.ПредыдущийКурс = 0 Или СтрокаКурса.Курс = 0 Тогда
		ИзменениеКурса = "0";
	Иначе
			
		ИзменениеКурсаЧислом = (СтрокаКурса.Курс / СтрокаКурса.Кратность - СтрокаКурса.ПредыдущийКурс 
					* СтрокаКурса.ПредыдущаяКратность) / СтрокаКурса.ПредыдущийКурс 
					* СтрокаКурса.ПредыдущаяКратность * 100;
		ИзменениеКурса = Формат(ИзменениеКурсаЧислом, "ЧДЦ=2;");
		
		Если ИзменениеКурсаЧислом > 0 Тогда
			СимволЗнака = "+";
		Иначе
			СимволЗнака = "";	
		КонецЕсли;	
			
		ИзменениеКурса = СимволЗнака + ИзменениеКурса;	
				
	КонецЕсли;	
		
	СтрокаКурса.СостояниеПредыдущегоКурса = СтрШаблон("%1 (%2, %3", ПрошлыйКурс, ДатаПредыдущегоКурса, ИзменениеКурса) + "%)";
	
КонецПроцедуры	

&НаКлиенте
Процедура ОбновитьОтображениеПредыдущегоКурса(ИмяИсточника)
	
	// получаем имя реквизита
	ИмяРеквизита = СтрРазделить(ИмяИсточника, "_")[1];
	ОтборИмяРеквизита = Новый Структура("ИмяРеквизита", ИмяРеквизита);
	СтрокаВалюты = ТаблицаКурсов.НайтиСтроки(ОтборИмяРеквизита)[0];
	
	СтрокаВалюты.Курс = ЭтотОбъект["Курс_" + ИмяРеквизита];
	СтрокаВалюты.Кратность = ЭтотОбъект["Кратность_" + ИмяРеквизита];
	ОбновитьСостояниеПредыдущегоКурса(СтрокаВалюты); 
	ЭтотОбъект["СостояниеПредыдущегоКурса_" + ИмяРеквизита] = СтрокаВалюты.СостояниеПредыдущегоКурса;
	
КонецПроцедуры	

#КонецОбласти