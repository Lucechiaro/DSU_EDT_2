#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Перем Расшифровка;
	
	Если Параметры.Свойство("Расшифровка", Расшифровка) И Расшифровка <> Неопределено Тогда
		НачалоПериода = Параметры.Расшифровка.НачалоПериода;
		КонецПериода = Параметры.Расшифровка.КонецПериода;
		СтатьяДДС = Параметры.Расшифровка.СтатьяДДС;
        СформироватьВручную();
	Иначе 
		НачалоПериода = ДобавитьМесяц(КонецДня(ТекущаяДатаСеанса()), -1) + 1;
		КонецПериода = КонецДня(ТекущаяДатаСеанса());
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;      
	
	СтруктураРасшифровки = ОбщегоНазначенияВызовСервера.ПолучитьСтруктуруРасшифровки(Расшифровка, ДанныеРасшифровки);
	
	Если СтруктураРасшифровки.Свойство("Документ") Тогда
		ОткрытьЗначениеАсинх(СтруктураРасшифровки.Документ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьВручнуюКлиент(Команда)
	
	СформироватьВручную();
	
КонецПроцедуры	

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьВручную()
	
	Результат.Очистить();
	
	ОтчетБД = РеквизитФормыВЗначение("Отчет");
	СхемаКомпоновкиДанных = ОтчетБД.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	ИсполняемыеНастройки = Отчет.КомпоновщикНастроек.Настройки;
	УстановитьНастройки(ИсполняемыеНастройки);
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	ДанныеРасшифровкиСКД = Новый ДанныеРасшифровкиКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, ИсполняемыеНастройки, ДанныеРасшифровкиСКД);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных, , ДанныеРасшифровкиСКД);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(Результат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	ДанныеРасшифровки = ПоместитьВоВременноеХранилище(ДанныеРасшифровкиСКД, УникальныйИдентификатор);
    	
	Результат.ПоказатьУровеньГруппировокСтрок(1);
	Результат.ПоказатьУровеньГруппировокСтрок(0);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНастройки(Настройки)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементПользовательскихНастроек(Настройки.ПараметрыДанных, "НачалоПериода", НачалоПериода);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементПользовательскихНастроек(Настройки.ПараметрыДанных, "КонецПериода", КонецПериода);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Настройки.Отбор, "СтатьяДДС", СтатьяДДС, ВидСравненияКомпоновкиДанных.ВИерархии);
	
КонецПроцедуры	

#КонецОбласти