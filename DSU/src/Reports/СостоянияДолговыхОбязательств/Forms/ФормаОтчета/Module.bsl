
&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтруктураРасшифровки = ОбщегоНазначенияКлиентСервер.ПолучитьСтруктуруРасшифровки(Расшифровка, ДанныеРасшифровки);
	
	Если СтруктураРасшифровки.Свойство("Должник") Тогда
		
		ФормаДетализации = ПолучитьФорму("Отчет.ДвиженияПоДолговымОбязательствам.Форма",,,Новый УникальныйИдентификатор);
		ПараметрыРасшифровки = Новый Структура;
		ПараметрыРасшифровки.Вставить("Должник", СтруктураРасшифровки.Должник);
		
		Валюта = Неопределено;
		СтруктураРасшифровки.Свойство("Валюта", Валюта);
		ПараметрыРасшифровки.Вставить("Валюта", Валюта);
		ПараметрыРасшифровки.Вставить("ВалютаНеЗаполнена", Не ЗначениеЗаполнено(Валюта));
		ПараметрыРасшифровки.Вставить("КонецПериода", Отчет.Дата);
		ПараметрыРасшифровки.Вставить("НачалоПериода", Неопределено);
		
		ФормаДетализации.ОбработатьРасшифровку(ФормаДетализации.Результат, ПараметрыРасшифровки);
		ФормаДетализации.Открыть();
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	
	СформироватьОтчетНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчетНаСервере()
	
	Результат.Очистить();
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	
	СхемаКомпоновкиДанных = ОтчетОбъект.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");

	ИсполняемыеНастройки = ОтчетОбъект.КомпоновщикНастроек.Настройки;
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДанных(ИсполняемыеНастройки.ПараметрыДанных, "Дата", ОтчетОбъект.Дата);
	
	ДанныеРасшифровкиСКД = Новый ДанныеРасшифровкиКомпоновкиДанных;
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, ИсполняемыеНастройки, ДанныеРасшифровкиСКД);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных, , ДанныеРасшифровкиСКД);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(Результат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	ДанныеРасшифровки = ПоместитьВоВременноеХранилище(ДанныеРасшифровкиСКД, УникальныйИдентификатор);
	
КонецПроцедуры	