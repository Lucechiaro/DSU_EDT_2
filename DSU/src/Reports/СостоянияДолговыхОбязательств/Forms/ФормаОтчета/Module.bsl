
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтруктураРасшифровки = ОбщегоНазначенияВызовСервера.ПолучитьСтруктуруРасшифровки(Расшифровка, ДанныеРасшифровки);
	
	Если СтруктураРасшифровки.Свойство("Должник") Тогда
		
		Валюта = Неопределено;
		СтруктураРасшифровки.Свойство("Валюта", Валюта);
		
		ПараметрыРасшифровки = Новый Структура;
		ПараметрыРасшифровки.Вставить("Должник", СтруктураРасшифровки.Должник);
		ПараметрыРасшифровки.Вставить("ОбработатьРасшифровку");
		ПараметрыРасшифровки.Вставить("Валюта", Валюта);
		ПараметрыРасшифровки.Вставить("ВалютаНеЗаполнена", Не ЗначениеЗаполнено(Валюта));
		ПараметрыРасшифровки.Вставить("КонецПериода", Отчет.Дата);
		ПараметрыРасшифровки.Вставить("НачалоПериода", Неопределено);
		
		ОткрытьФорму("Отчет.ДвиженияПоДолговымОбязательствам.Форма", ПараметрыРасшифровки,, Новый УникальныйИдентификатор);
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	
	СформироватьОтчетНаСервере();
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьОтчетНаСервере()
	
	Результат.Очистить();
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	
	СхемаКомпоновкиДанных = ОтчетОбъект.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");

	ИсполняемыеНастройки = ОтчетОбъект.КомпоновщикНастроек.Настройки;
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДанных(ИсполняемыеНастройки.ПараметрыДанных, "Дата", ОтчетОбъект.Дата);
	
	ДанныеРасшифровкиСКД = Новый ДанныеРасшифровкиКомпоновкиДанных;
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, ИсполняемыеНастройки, ДанныеРасшифровкиСКД);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных, , ДанныеРасшифровкиСКД);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(Результат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	ДанныеРасшифровки = ПоместитьВоВременноеХранилище(ДанныеРасшифровкиСКД, УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти






	