
&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	СформироватьТабличныйДокумент();
	
КонецПроцедуры

Процедура СформироватьТабличныйДокумент() Экспорт 
	
	Перем ТабДоходы, ТабРасходы, ДоходыИтог, РасходыИтог;
	
	ОтчетБД = РеквизитФормыВЗначение("Отчет");;
	ТабличныйДокумент = Новый ТабличныйДокумент;
	Макет = ОтчетБД.ПолучитьМакет("Макет");
	
	ПодготовитьТаблицуДанных("Доходы", ТабДоходы, ДоходыИтог);
	ПодготовитьТаблицуДанных("Расходы", ТабРасходы, РасходыИтог);
	
	// выведем отчет
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьИтог = Макет.ПолучитьОбласть("Итог");
	ОбластьСальдо = Макет.ПолучитьОбласть("Сальдо");
	
	ОбластьШапка.Параметры.ТекстШапкиБаланса = "Сальдо за период с " + Формат(Отчет.НачалоПериода, "ДФ='dd MMMM'")  + " по " + Формат(Отчет.КонецПериода, "ДФ='dd MMMM'");	
	ТабличныйДокумент.Вывести(ОбластьШапка);
	ТабличныйДокумент.Вывести(ОбластьЗаголовок);
	
	МаксСтрокаДоходы = табДоходы.Количество() - 1;
	МаксСтрокаРасходы = табРасходы.Количество() - 1;

	КоличествоСтрок = Макс(МаксСтрокаДоходы, МаксСтрокаРасходы);
	
	Для НомерСтроки = 0 По КоличествоСтрок Цикл
		
		Если НомерСтроки > МаксСтрокаДоходы Тогда
			ОбластьСтрока.Параметры.СтатьяДДСДоходы = "";
			ОбластьСтрока.Параметры.СуммаДоходы = "";
			ОбластьСтрока.Параметры.ПроцентДоходы = "";
			ОбластьСтрока.Параметры.РасшифровкаДоходы = Неопределено;
		Иначе
			СтрокаДанных = табДоходы[НомерСтроки];
			ОбластьСтрока.Параметры.СтатьяДДСДоходы = СтрокаДанных.СтатьяДДС;
			ОбластьСтрока.Параметры.СуммаДоходы = СтрокаДанных.Сумма;
			ОбластьСтрока.Параметры.ПроцентДоходы = СтрокаДанных.Процент;
			ОбластьСтрока.Параметры.РасшифровкаДоходы = ПодготовитьПараметрыРасшифровки("Доходы", СтрокаДанных.СтатьяДДС);
		КонецЕсли;
		
		Если НомерСтроки > МаксСтрокаРасходы Тогда
			ОбластьСтрока.Параметры.СтатьяДДСРасходы = "";
			ОбластьСтрока.Параметры.СуммаРасходы = "";
			ОбластьСтрока.Параметры.ПроцентРасходы = "";
			ОбластьСтрока.Параметры.РасшифровкаРасходы = Неопределено;
		Иначе
			СтрокаДанных = табРасходы[НомерСтроки];
			ОбластьСтрока.Параметры.СтатьяДДСРасходы = СтрокаДанных.СтатьяДДС ;
			ОбластьСтрока.Параметры.СуммаРасходы = СтрокаДанных.Сумма;
			ОбластьСтрока.Параметры.ПроцентРасходы = СтрокаДанных.Процент;
			ОбластьСтрока.Параметры.РасшифровкаРасходы = ПодготовитьПараметрыРасшифровки("Расходы", СтрокаДанных.СтатьяДДС);
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьСтрока);
		
	КонецЦикла;
	
	ОбластьИтог.Параметры.СуммаДоходы = ДоходыИтог;
	ОбластьИтог.Параметры.РасшифровкаДоходы = ПодготовитьПараметрыРасшифровки("Доходы", Неопределено);
	ОбластьИтог.Параметры.СуммаРасходы = РасходыИтог;
	ОбластьИтог.Параметры.РасшифровкаРасходы = ПодготовитьПараметрыРасшифровки("Расходы", Неопределено);
	ТабличныйДокумент.Вывести(ОбластьИтог);
	
	ОбластьСальдо.Параметры.СуммаДоходы = ДоходыИтог;
	ОбластьСальдо.Параметры.СуммаРасходы = РасходыИтог;
	ОбластьСальдо.Параметры.Сальдо = ДоходыИтог - РасходыИтог;
	ОбластьСальдо.Параметры.РасшифровкаДоходы = ОбластьИтог.Параметры.РасшифровкаДоходы;
    ОбластьСальдо.Параметры.РасшифровкаРасходы = ОбластьИтог.Параметры.РасшифровкаРасходы;
	ТабличныйДокумент.Вывести(ОбластьСальдо);
	
	Результат.Очистить();
	Результат.Вывести(ТабличныйДокумент);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отчет.НачалоПериода = ДобавитьМесяц(КонецДня(ТекущаяДата()) + 1, -1);
	Отчет.КонецПериода = ТекущаяДата();
	
КонецПроцедуры

Процедура ПодготовитьТаблицуДанных(ИмяРегистра, ТЗ, СуммаИтог)
	
	// создадим таблицы для наполнения
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("СтатьяДДС");
	ТЗ.Колонки.Добавить("ВерхнийУровень");
	ТЗ.Колонки.Добавить("Сумма");
	ТЗ.Колонки.Добавить("Процент");
	
	// получим данные из регистра
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РегистрОбороты.СтатьяДДС КАК СтатьяДДС,
	               |	ВЫБОР
	               |		КОГДА РегистрОбороты.СтатьяДДС.Родитель = ЗНАЧЕНИЕ(Справочник.СтатьиДДС.ПустаяСсылка)
	               |			ТОГДА ИСТИНА
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ КАК ВерхнийУровень,
	               |	РегистрОбороты.СуммаУчетнОборот КАК Сумма
	               |ИЗ
	               |	РегистрНакопления." + ИмяРегистра + ".Обороты(&НачалоПериода, &КонецПериода) КАК РегистрОбороты
	               |ИТОГИ
	               |	СУММА(Сумма)
	               |ПО
	               |	СтатьяДДС ТОЛЬКО ИЕРАРХИЯ";
				   
	Запрос.УстановитьПараметр("НачалоПериода", ?(ЗначениеЗаполнено(Отчет.НачалоПериода), НачалоДня(Отчет.НачалоПериода), Дата(1,1,1)));
	Запрос.УстановитьПараметр("КонецПериода", ?(ЗначениеЗаполнено(Отчет.КонецПериода), КонецДня(Отчет.КонецПериода), Дата(3000,1,1)));
	Выборка = Запрос.Выполнить().Выбрать();
	
	// сформируем таблицу данных
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.СтатьяДДС) И (Выборка.ВерхнийУровень = Null Или Выборка.ВерхнийУровень) Тогда
			НоваяСтрока = ТЗ.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		КонецЕсли;	
	КонецЦикла;	
	
	ТЗ.Сортировать("Сумма Убыв");
	СуммаИтог = ТЗ.Итог("Сумма");
	
	// рассчитаем проценты
	Для Каждого Строка Из ТЗ Цикл 
		 Строка.Процент = Окр(Строка.Сумма / СуммаИтог * 100, 2);
	КонецЦикла;	
				   
КонецПроцедуры	

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	Если ТипЗнч(Расшифровка) = Тип("Структура") Тогда
		СформироватьОтчетПоДетализации(Расшифровка.ТипОтчета, Расшифровка.СтатьяДДС);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;	
	
КонецПроцедуры

Функция ПодготовитьПараметрыРасшифровки(ТипОтчета, СтатьяДДС)
	
	Расшифровка = Новый Структура;
	Расшифровка.Вставить("ТипОтчета", ТипОтчета);
	Расшифровка.Вставить("СтатьяДДС", СтатьяДДС);
	
	Возврат Расшифровка
	
КонецФункции	

&НаКлиенте
Процедура СформироватьОтчетПоДетализации(ТипОтчета, СтатьяДДС)
	
	ПараметрыРасшифровки = Новый Структура();
	ПараметрыРасшифровки.Вставить("НачалоПериода", Отчет.НачалоПериода);
	ПараметрыРасшифровки.Вставить("КонецПериода", Отчет.КонецПериода);
	ПараметрыРасшифровки.Вставить("СтатьяДДС", СтатьяДДС);
	
	Если ТипОтчета = "Доходы" Тогда
		ИмяОткрываемойФормы = "Отчет.ДоходыЗаПериод.Форма";
	ИначеЕсли ТипОтчета = "Расходы" Тогда
		ИмяОткрываемойФормы = "Отчет.РасходыЗаПериод.Форма";
	КонецЕсли;
	
	ОткрытьФорму(ИмяОткрываемойФормы, Новый Структура("Расшифровка", ПараметрыРасшифровки), , Истина);
	
КонецПроцедуры	

