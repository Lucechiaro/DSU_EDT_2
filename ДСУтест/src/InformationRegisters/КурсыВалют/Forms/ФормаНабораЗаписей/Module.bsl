
&НаКлиенте
Процедура ОбновитьНадписьКурсыАктуальны() Экспорт
	
	НадписьКА = Элементы.НадписьКурсыВалют;
	 
	Если КурсыВалютАктуальны(ТекущаяДата()) Тогда
		НадписьКА.Заголовок = "Курсы валют актуальны";
		НадписьКА.ЦветТекста = WebЦвета.Зеленый;
	Иначе
	    НадписьКА.Заголовок = "Курсы валют не актуальны! Исправить!";
		НадписьКА.ЦветТекста = WebЦвета.Красный;
	КонецЕсли;	 
	
КонецПроцедуры	


&НаКлиенте
Процедура НадписьКурсыВалютНажатие(Элемент)
	
	ФормаВводаКурсов = ПолучитьФорму("ОбщаяФорма.ФормаВводаКурсовВалют");
	ФормаВводаКурсов.ДатаКурсов = ТекущаяДата();

	Если Не ФормаВводаКурсов.Открыта() Тогда
		ФормаВводаКурсов.ОткрытьМодально();
	КонецЕсли;
	
	ОбновитьНадписьКурсыАктуальны();
	
КонецПроцедуры

&НаКлиенте
Процедура КурсыПриИзменении(Элемент)
	ОбновитьНадписьКурсыАктуальны();	
КонецПроцедуры

&НаКлиенте
Процедура КурсыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ОбновитьНадписьКурсыАктуальны();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьНадписьКурсыАктуальны();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьТаблицуАктуальностиКурсов(ДатаКурсов, флгТолькоНеактуальныеКурсы = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Период,
	|	КурсыВалютСрезПоследних.Валюта
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(
	|			&ДатаКурсов, Валюта В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Остатки.Валюта
	|				ИЗ
	|					РегистрНакопления.ОстаткиПоСчетам.Остатки(&ДатаКурсов, Валюта.Используется И Валюта <> &ВалютаУчета) КАК Остатки)
	|			) КАК КурсыВалютСрезПоследних"
	+ ?(флгТолькоНеактуальныеКурсы, " Где КурсыВалютСрезПоследних.Период < НачалоПериода(&ДатаКурсов, День)", "");
	Запрос.УстановитьПараметр("ДатаКурсов", КонецДня(ДатаКурсов));
	Запрос.УстановитьПараметр("ВалютаУчета", Константы.ОсновнаяВалютаУчета.Получить());
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции	                                             

&НаСервереБезКонтекста
Функция КурсыВалютАктуальны(ДатаКурсов) Экспорт 
	
	ТаблицаКурсов = ПолучитьТаблицуАктуальностиКурсов(ДатаКурсов, Истина);
	Возврат ТаблицаКурсов.Количество() = 0;
	
КонецФункции	
