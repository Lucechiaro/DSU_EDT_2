
Функция Сформировать() Экспорт
	
	Макет = ПолучитьМакет("Макет");
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапка.Параметры.ДатаПрогноза = Формат(ДатаПрогноза, "ДФ='dd MMMM yyyy'");
	ТабличныйДокумент.Вывести(ОбластьШапка);
	
	// выведем статьи с незаполненными настройками бюджетирования
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СтатьиДДС.Ссылка КАК СтатьяДДС
	               |ИЗ
	               |	Справочник.СтатьиДДС КАК СтатьиДДС
	               |ГДЕ
	               |	СтатьиДДС.ЭтоГруппа = ЛОЖЬ
	               |	И СтатьиДДС.ПланируетсяПоСреднему = ЗНАЧЕНИЕ(Перечисление.БулевоДаНет.ПустаяСсылка)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Наименование";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() > 0 Тогда
		
		ОбластьЗаголовокСтатьи = Макет.ПолучитьОбласть("ЗаголовокНенастроенныеСтатьи");
		ОбластьСтатья = Макет.ПолучитьОбласть("СтрокаНенастроенныеСтатьи");
		ТабличныйДокумент.Вывести(ОбластьЗаголовокСтатьи);
		
		Пока Выборка.Следующий() Цикл
			ОбластьСтатья.Параметры.Заполнить(Выборка);
			ТабличныйДокумент.Вывести(ОбластьСтатья);
		КонецЦикла;
		
	КонецЕсли;	
	
	// получим плановые расходы по планируемым статьям
	Коэффициент = (ДатаПрогноза - ДатаОтчета) / (ДатаОтчета - ДобавитьМесяц(ДатаОтчета, - КоличествоМесяцевДляАнализа));
    Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	               |	КурсыВалютСрезПоследних.Курс КАК Курс,
	               |	КурсыВалютСрезПоследних.Кратность КАК Кратность
	               |ПОМЕСТИТЬ КурсыВалют
	               |ИЗ
	               |	РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОтчета, ) КАК КурсыВалютСрезПоследних
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СУММА(ВЫБОР
	               |			КОГДА Счета.Ликвидный
	               |				ТОГДА ДенежныеСредстваОстатки.СуммаОстаток * КурсыВалют.Курс / КурсыВалют.Кратность
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК Сумма
	               |ИЗ
	               |	РегистрНакопления.ОстаткиПоСчетам.Остатки(
	               |			&ДатаОтчета,
	               |			Счет.Забалансовый = &СчетЯвляетсяЗабалансовым
	               |				ИЛИ &ПоказыватьВсеСчета) КАК ДенежныеСредстваОстатки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	               |		ПО ДенежныеСредстваОстатки.Валюта = КурсыВалют.Валюта
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Счета КАК Счета
	               |		ПО ДенежныеСредстваОстатки.Счет = Счета.Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВЗ.ВидДвижения КАК ВидДвижения,
	               |	ВЗ.СтатьяДДС КАК СтатьяДДС,
	               |	ВЗ.Комментарий КАК Комментарий,
	               |	СУММА(ВЗ.СуммаАбсолютная) КАК СуммаАбсолютная,
	               |	СУММА(ВЗ.Сумма) КАК Сумма
	               |ПОМЕСТИТЬ ИтоговаяТаблица
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		ЗНАЧЕНИЕ(Перечисление.ВидДвиженияДС.Расход) КАК ВидДвижения,
	               |		РасходыОбороты.СтатьяДДС КАК СтатьяДДС,
	               |		РасходыОбороты.СуммаОборот * КурсыВалют.Курс / КурсыВалют.Кратность * &Коэффициент КАК СуммаАбсолютная,
	               |		-РасходыОбороты.СуммаОборот * КурсыВалют.Курс / КурсыВалют.Кратность * &Коэффициент КАК Сумма,
	               |		"""" КАК Комментарий
	               |	ИЗ
	               |		РегистрНакопления.Расходы.Обороты(&НачалоПериода, &ОкончаниеПериода, , СтатьяДДС.ПланируетсяПоСреднему = ЗНАЧЕНИЕ(Перечисление.БулевоДаНет.Да)) КАК РасходыОбороты
	               |			ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	               |			ПО РасходыОбороты.Валюта = КурсыВалют.Валюта
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		ЗНАЧЕНИЕ(Перечисление.ВидДвиженияДС.Расход),
	               |		Расходы.СтатьяДДС,
	               |		-Расходы.Сумма * КурсыВалют.Курс / КурсыВалют.Кратность * &Коэффициент,
	               |		Расходы.Сумма * КурсыВалют.Курс / КурсыВалют.Кратность * &Коэффициент,
	               |		""""
	               |	ИЗ
	               |		РегистрНакопления.Расходы КАК Расходы
	               |			ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	               |			ПО Расходы.Валюта = КурсыВалют.Валюта
	               |	ГДЕ
	               |		Расходы.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	               |		И Расходы.СтатьяДДС.ПланируетсяПоСреднему = ЗНАЧЕНИЕ(Перечисление.БулевоДаНет.Да)
	               |		И Расходы.ИсключитьИзПланирования
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		ЗНАЧЕНИЕ(Перечисление.ВидДвиженияДС.Доход),
	               |		ДоходыОбороты.СтатьяДДС,
	               |		ДоходыОбороты.СуммаОборот * КурсыВалют.Курс / КурсыВалют.Кратность * &Коэффициент,
	               |		ДоходыОбороты.СуммаОборот * КурсыВалют.Курс / КурсыВалют.Кратность * &Коэффициент,
	               |		""""
	               |	ИЗ
	               |		РегистрНакопления.Доходы.Обороты(&НачалоПериода, &ОкончаниеПериода, , СтатьяДДС.ПланируетсяПоСреднему = ЗНАЧЕНИЕ(Перечисление.БулевоДаНет.Да)) КАК ДоходыОбороты
	               |			ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	               |			ПО ДоходыОбороты.Валюта = КурсыВалют.Валюта
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		ЗНАЧЕНИЕ(Перечисление.ВидДвиженияДС.Доход),
	               |		Доходы.СтатьяДДС,
	               |		-Доходы.Сумма * КурсыВалют.Курс / КурсыВалют.Кратность * &Коэффициент,
	               |		Доходы.Сумма * КурсыВалют.Курс / КурсыВалют.Кратность * &Коэффициент,
	               |		""""
	               |	ИЗ
	               |		РегистрНакопления.Доходы КАК Доходы
	               |			ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	               |			ПО Доходы.Валюта = КурсыВалют.Валюта
	               |	ГДЕ
	               |		Доходы.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	               |		И Доходы.СтатьяДДС.ПланируетсяПоСреднему = ЗНАЧЕНИЕ(Перечисление.БулевоДаНет.Да)
	               |		И Доходы.ИсключитьИзПланирования
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		ГрафикПлановыхДоходовРасходов.ВидДвижения,
	               |		ГрафикПлановыхДоходовРасходов.Статья,
	               |		СУММА(ГрафикПлановыхДоходовРасходов.Сумма),
	               |		СУММА(ВЫБОР
	               |				КОГДА ГрафикПлановыхДоходовРасходов.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.ВидДвиженияДС.Доход)
	               |					ТОГДА 1
	               |				ИНАЧЕ -1
	               |			КОНЕЦ * ГрафикПлановыхДоходовРасходов.Сумма),
	               |		ГрафикПлановыхДоходовРасходов.Комментарий
	               |	ИЗ
	               |		РегистрСведений.ГрафикПлановыхДоходовРасходов КАК ГрафикПлановыхДоходовРасходов
	               |	ГДЕ
	               |		ГрафикПлановыхДоходовРасходов.Период МЕЖДУ &ДатаОтчета И &ДатаПрогноза
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		ГрафикПлановыхДоходовРасходов.ВидДвижения,
	               |		ГрафикПлановыхДоходовРасходов.Статья,
	               |		ГрафикПлановыхДоходовРасходов.Комментарий) КАК ВЗ
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВЗ.ВидДвижения,
	               |	ВЗ.СтатьяДДС,
	               |	ВЗ.Комментарий
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ИтоговаяТаблица.ВидДвижения КАК ВидДвижения,
	               |	ИтоговаяТаблица.СтатьяДДС КАК СтатьяДДС,
	               |	ИтоговаяТаблица.Комментарий КАК Комментарий,
	               |	ИтоговаяТаблица.СуммаАбсолютная КАК СуммаАбсолютная,
	               |	ИтоговаяТаблица.Сумма КАК Сумма,
	               |	ВЫБОР
	               |		КОГДА СтатьиДДС.ПланируетсяПоСреднему = ЗНАЧЕНИЕ(Перечисление.БулевоДаНет.Да)
	               |			ТОГДА ""По среднему""
	               |		КОГДА СтатьиДДС.ПланируетсяПоСреднему = ЗНАЧЕНИЕ(Перечисление.БулевоДаНет.Нет)
	               |			ТОГДА ""По плану""
	               |		ИНАЧЕ """"
	               |	КОНЕЦ КАК ТипПланирования
	               |ИЗ
	               |	ИтоговаяТаблица КАК ИтоговаяТаблица
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтатьиДДС КАК СтатьиДДС
	               |		ПО ИтоговаяТаблица.СтатьяДДС = СтатьиДДС.Ссылка
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ВидДвижения,
	               |	СуммаАбсолютная УБЫВ
	               |ИТОГИ
	               |	СУММА(СуммаАбсолютная),
	               |	СУММА(Сумма),
	               |	МАКСИМУМ(ТипПланирования)
	               |ПО
	               |	ОБЩИЕ,
	               |	ВидДвижения,
	               |	СтатьяДДС ИЕРАРХИЯ
	               |АВТОУПОРЯДОЧИВАНИЕ";
	Запрос.УстановитьПараметр("НачалоПериода", ДобавитьМесяц(ДатаОтчета, - КоличествоМесяцевДляАнализа));
	Запрос.УстановитьПараметр("ОкончаниеПериода", ДатаОтчета);
	Запрос.УстановитьПараметр("ДатаОтчета", ДатаОтчета);
	Запрос.УстановитьПараметр("ДатаПрогноза", ДатаПрогноза);
	Запрос.УстановитьПараметр("Коэффициент", Коэффициент);
	Запрос.УстановитьПараметр("СчетЯвляетсяЗабалансовым", Ложь);
	Запрос.УстановитьПараметр("ПоказыватьВсеСчета", Ложь);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();

	ОбластьНачальныйОстаток = Макет.ПолучитьОбласть("НачальныйОстаток");
	ОбластьВидДвижения = Макет.ПолучитьОбласть("ВидДвижения");
	ОбластьСтатья = Макет.ПолучитьОбласть("Статья");
	ОбластьСтатьяИерархия = Макет.ПолучитьОбласть("СтатьяИерархия");
	ОбластьКонечныйОстаток = Макет.ПолучитьОбласть("КонечныйОстаток");
	ОбластьДетали = Макет.ПолучитьОбласть("Детали");
	
	ВыборкаНачальныйОстаток = РезультатЗапроса[1].Выбрать();
	ВыборкаНачальныйОстаток.Следующий();
	НачальныйОстаток = ?(ВыборкаНачальныйОстаток.Сумма = Null, 0, ВыборкаНачальныйОстаток.Сумма);
	ОбластьНачальныйОстаток.Параметры.Сумма = НачальныйОстаток;
	ТабличныйДокумент.Вывести(ОбластьНачальныйОстаток);
	
	ВыборкаДвиженияИтог = РезультатЗапроса[3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаДвиженияИтог.Следующий();
	КонечныйОстаток = НачальныйОстаток + ?(ЗначениеЗаполнено(ВыборкаДвиженияИтог.Сумма), ВыборкаДвиженияИтог.Сумма, 0);
	
	ВыборкаВидДвижения = ВыборкаДвиженияИтог.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	СтэкУровнейГруппировки = Новый Соответствие;
	
	Пока ВыборкаВидДвижения.Следующий() Цикл
		
		ТабличныйДокумент.НачатьАвтогруппировкуСтрок();
		ОбластьВидДвижения.Параметры.Заполнить(ВыборкаВидДвижения);
		ТабличныйДокумент.Вывести(ОбластьВидДвижения, 1);
		
		ВыборкаСтатьяДДС = ВыборкаВидДвижения.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией, "СтатьяДДС");
		
		НачальныйУровень = ВыборкаСтатьяДДС.Уровень();
		ТекущийУровеньГруппировки = НачальныйУровень;
		СтэкУровнейГруппировки.Вставить(ТекущийУровеньГруппировки, ВыборкаСтатьяДДС);
		
		Пока Истина Цикл
			
			ВыборкаСтатьяДДС = СтэкУровнейГруппировки.Получить(ТекущийУровеньГруппировки);
			
			Если ВыборкаСтатьяДДС.Следующий() Тогда
				
				ТекущаяОбласть = ?(ВыборкаСтатьяДДС.СтатьяДДС.ЭтоГруппа, ОбластьСтатьяИерархия, ОбластьСтатья);
				
				ТекущаяОбласть.Параметры.Заполнить(ВыборкаСтатьяДДС);
				ТекущаяОбласть.Параметры.Табуляция = СформироватьТабуляцию(ТекущийУровеньГруппировки);
				ТабличныйДокумент.Вывести(ТекущаяОбласть, ТекущийУровеньГруппировки);
				
				Если Не ВыборкаСтатьяДДС.СтатьяДДС.ЭтоГруппа 
					//И Не ВыборкаСтатьяДДС.СтатьяДДС.ПланируетсяПоСреднему = Перечисления.БулевоДаНет.Да 
					Тогда
					
					ВыборкаДетали = ВыборкаСтатьяДДС.Выбрать();
					
					Пока ВыборкаДетали.Следующий() Цикл
						
						ОбластьДетали.Параметры.Заполнить(ВыборкаДетали);
						ОбластьДетали.Параметры.Табуляция = СформироватьТабуляцию(ТекущийУровеньГруппировки + 1);
						ТабличныйДокумент.Вывести(ОбластьДетали, ТекущийУровеньГруппировки + 1);
						
					КонецЦикла;
					
				КонецЕсли;	
				
				ПодчиненнаяВыборка = ВыборкаСтатьяДДС.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией, "СтатьяДДС");
				
				Если ПодчиненнаяВыборка.Количество() > 0 Тогда
					
					ТекущийУровеньГруппировки = ТекущийУровеньГруппировки + 1;
					СтэкУровнейГруппировки.Вставить(ТекущийУровеньГруппировки, ПодчиненнаяВыборка);
					
				КонецЕсли;	
				
			Иначе
				
				Если ВыборкаСтатьяДДС.Уровень() = НачальныйУровень Тогда
					Прервать;
				КонецЕсли;	
				
				ТекущийУровеньГруппировки = ТекущийУровеньГруппировки - 1;
				
			КонецЕсли;	
			
		КонецЦикла;	
		
		ТабличныйДокумент.ЗакончитьАвтогруппировкуСтрок();
		
	КонецЦикла;
	
	ТабличныйДокумент.ПоказатьУровеньГруппировокСтрок(1);
	
	ОбластьДельта = Макет.ПолучитьОбласть("Дельта");
	ОбластьДельта.Параметры.Сумма = ?(ЗначениеЗаполнено(ВыборкаДвиженияИтог.Сумма), ВыборкаДвиженияИтог.Сумма, 0);
	ТабличныйДокумент.Вывести(ОбластьДельта);
	
	ОбластьКонечныйОстаток.Параметры.Сумма = КонечныйОстаток;
	ТабличныйДокумент.Вывести(ОбластьКонечныйОстаток);
	
	// выведем плановые расходы
	ОбластьЗаголовок = Макет.ПолучитьОбласть("ПлановыеРасходыЗаголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("ПлановыеРасходыШапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("ПлановыеРасходыСтрока");
	ОбластьПодвал = Макет.ПолучитьОбласть("ПлановыеРасходыПодвал");
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ГрафикПлановыхДоходовРасходов.Период КАК Дата,
	               |	ГрафикПлановыхДоходовРасходов.Статья КАК Статья,
	               |	ГрафикПлановыхДоходовРасходов.Сумма КАК Сумма,
	               |	ГрафикПлановыхДоходовРасходов.Комментарий КАК Комментарий
	               |ИЗ
	               |	РегистрСведений.ГрафикПлановыхДоходовРасходов КАК ГрафикПлановыхДоходовРасходов
	               |ГДЕ
	               |	ГрафикПлановыхДоходовРасходов.Период МЕЖДУ &ДатаОтчета И &ДатаПрогноза
	               |	И ГрафикПлановыхДоходовРасходов.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.ВидДвиженияДС.Расход)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Период,
	               |	Сумма УБЫВ
	               |ИТОГИ
	               |	СУММА(Сумма)
	               |ПО
	               |	ОБЩИЕ";
	
	ВыборкаИтог = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаИтог.Следующий();
	ВыборкаДетали = ВыборкаИтог.Выбрать();
	
	ТабличныйДокумент.Вывести(ОбластьЗаголовок);
	ТабличныйДокумент.Вывести(ОбластьШапка);
	
	Пока ВыборкаДетали.Следующий() Цикл
		
		ОбластьСтрока.Параметры.Заполнить(ВыборкаДетали);
		ТабличныйДокумент.Вывести(ОбластьСтрока);
		
	КонецЦикла;	
	
	ОбластьПодвал.Параметры.Заполнить(ВыборкаИтог);
	ТабличныйДокумент.Вывести(ОбластьПодвал);
	
	Возврат ТабличныйДокумент
	
КонецФункции	

Функция СформироватьТабуляцию(ЧислоСимволов)
	
	Результат = "";
	
	Для Номер = 1 По ЧислоСимволов Цикл 
		Результат = Результат + "  ";
	КонецЦикла;	
	
	Возврат Результат
	
КонецФункции	