
&НаКлиенте
Процедура СформироватьВручнуюКлиент(Команда)
	
	СформироватьВручную();
	
КонецПроцедуры	

&НаСервере
Функция СформироватьВручную() Экспорт
	
	Результат.Очистить();
	
	ОтчетБД = РеквизитФормыВЗначение("Отчет");
	ИсполняемыеНастройки = ОтчетБД.КомпоновщикНастроек.Настройки;
	УстановитьНастройки(ИсполняемыеНастройки);
	
	СхемаКомпоновкиДанных = ОтчетБД.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	ИсполняемыеНастройки = ОтчетБД.КомпоновщикНастроек.Настройки;
	ДанныеРасшифровкиСКД = Новый ДанныеРасшифровкиКомпоновкиДанных;
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, ИсполняемыеНастройки, ДанныеРасшифровкиСКД);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных, , ДанныеРасшифровкиСКД);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(Результат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	ДанныеРасшифровки = ПоместитьВоВременноеХранилище(ДанныеРасшифровкиСКД, УникальныйИдентификатор);
	Результат.ПоказатьУровеньГруппировокСтрок(2);
	Результат.ПоказатьУровеньГруппировокСтрок(1);
	Результат.ПоказатьУровеньГруппировокСтрок(0);
	
КонецФункции	

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Перем Расшифровка;
	
	Если Параметры.Свойство("Расшифровка", Расшифровка) И Расшифровка <> Неопределено Тогда
		НачалоПериода = Параметры.Расшифровка.НачалоПериода;
		КонецПериода = Параметры.Расшифровка.КонецПериода;
		СтатьяДДС = Параметры.Расшифровка.СтатьяДДС;
        СформироватьВручную();
	Иначе 
		НачалоПериода = ДобавитьМесяц(КонецДня(ТекущаяДата()), -1) + 1;
		КонецПериода = КонецДня(ТекущаяДата());
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьНастройки(Настройки)
	
	ОбщегоНазначения.УстановитьЭлементПользовательскихНастроек(Настройки.ПараметрыДанных, "НачалоПериода", НачалоПериода);
	ОбщегоНазначения.УстановитьЭлементПользовательскихНастроек(Настройки.ПараметрыДанных, "КонецПериода", КонецПериода);
	
	Если СтатьяДДС <> Неопределено Тогда
		ОбщегоНазначения.УстановитьЭлементОтбора(Настройки.Отбор, "СтатьяДДС", СтатьяДДС, ВидСравненияКомпоновкиДанных.ВИерархии);
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;      
	
	СтруктураРасшифровки = ОбщегоНазначенияСервер.ПолучитьСтруктуруРасшифровки(Расшифровка, ЭтаФорма.ДанныеРасшифровки);
	
	Если СтруктураРасшифровки.Свойство("Документ") Тогда
		ОткрытьЗначение(СтруктураРасшифровки.Документ);
	КонецЕсли;
	
КонецПроцедуры
