
&НаСервере
Функция ПолучитьТаблицуАктуальностиКурсов(ДатаКурсов, флгТолькоНеактуальныеКурсы = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Период,
	|	КурсыВалютСрезПоследних.Валюта
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(
	|			&ДатаКурсов, Валюта В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Остатки.Валюта
	|				ИЗ
	|					РегистрНакопления.ОстаткиПоСчетам.Остатки(&ДатаКурсов, Валюта.Используется И Валюта <> &ВалютаУчета) КАК Остатки)
	|			) КАК КурсыВалютСрезПоследних"
	+ ?(флгТолькоНеактуальныеКурсы, " Где КурсыВалютСрезПоследних.Период < НачалоПериода(&ДатаКурсов, День)", "");
	Запрос.УстановитьПараметр("ДатаКурсов", КонецДня(ДатаКурсов));
	Запрос.УстановитьПараметр("ВалютаУчета", Константы.ОсновнаяВалютаУчёта.Получить());
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции	                                             

&НаСервере
Функция КурсыВалютАктуальны(ДатаКурсов) Экспорт 
	
	ТаблицаКурсов = ПолучитьТаблицуАктуальностиКурсов(ДатаКурсов, Истина);
	Возврат ТаблицаКурсов.Количество() = 0;
	
КонецФункции

// проматываем элемент расшифровки вверх в поисках родителей
Функция ПолучитьСтруктуруРасшифровки(Расшифровка, ДанныеРасшифровки) Экспорт 
	
	СтруктураРасшифровки = Новый Структура;
	Коллекция = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	Элемент = Коллекция.Элементы[Расшифровка];
	ОбработатьПолеРасшифровки(Элемент, СтруктураРасшифровки);
	
	Возврат СтруктураРасшифровки
	
КонецФункции

Процедура ОбработатьПолеРасшифровки(Элемент, СтруктураРасшифровки) Экспорт 

	Если ТипЗнч(Элемент) = Тип("ЭлементРасшифровкиКомпоновкиДанныхПоля") Тогда 
		
		ПоляЭлемента = Элемент.ПолучитьПоля();	
		
		Для Каждого ПолеЭлемента Из ПоляЭлемента Цикл
			Попытка
				СтруктураРасшифровки.Вставить(СтрЗаменить(ПолеЭлемента.Поле, ".", ""), ПолеЭлемента.Значение);
			Исключение
			КонецПопытки;
		КонецЦикла;	
		
	КонецЕсли;
	
	РодителиПоля = Элемент.ПолучитьРодителей();
	
	Для Каждого РодительПоля Из РодителиПоля Цикл
		ОбработатьПолеРасшифровки(РодительПоля, СтруктураРасшифровки)
	КонецЦикла;	
	
КонецПроцедуры