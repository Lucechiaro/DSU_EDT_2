
&НаСервере
// ОбновитьНапоминания - формирует новые задачи на основе данных справочника "Напоминания" 
Процедура ОбновитьНапоминания() Экспорт
	
	 СоздатьОднократныеНапоминания();
	 СоздатьНапоминанияПоКоличествуПовторов();
	 СоздатьБесконечныеНапоминания();
	
КонецПроцедуры	

// СоздатьОднократныеНапоминания - создает задачи на основе однократных напоминаний и 
// устанавливает напоминаниям статус "Завершено".
&НаСервере
Процедура СоздатьОднократныеНапоминания()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	НастройкиНапоминаний.Наименование,
	|	НастройкиНапоминаний.ДатаНачала КАК Дата,
	|	НастройкиНапоминаний.Ссылка КАК Основание,
	|	НастройкиНапоминаний.СуммаПлатежа
	|ИЗ
	|	Справочник.НастройкиНапоминаний КАК НастройкиНапоминаний
	|ГДЕ
	|	НастройкиНапоминаний.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияНапоминаний.Активно)
	|	И НастройкиНапоминаний.Периодическое = ЛОЖЬ
	|	И НастройкиНапоминаний.ДатаОкончания <= &ДатаНапоминания";
	Запрос.УстановитьПараметр("ДатаНапоминания", НачалоДня(ТекущаяДата()) + 60*60*24*Константы.НапоминанияДнейДоСобытия.Получить());				   
	
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Задача = Задачи.Напоминание.СоздатьЗадачу();
		ЗаполнитьЗначенияСвойств(Задача, Выборка);
		Задача.Записать();
		
		// сразу пометим напоминание как завершенное
		НапоминаниеОбъект = Выборка.Основание.ПолучитьОбъект();
		НапоминаниеОбъект.Состояние = Перечисления.СостоянияНапоминаний.Завершено;
		НапоминаниеОбъект.Записать();
		
	КонецЦикла;	
	
КонецПроцедуры

// СоздатьНапоминанияПоКоличествуПовторов - создает задачи на основе периодических напоминаний,
// для которых задано точное количество повторов
&НаСервере
Процедура СоздатьНапоминанияПоКоличествуПовторов()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	НастройкиНапоминаний.Ссылка КАК Ссылка
	               |ПОМЕСТИТЬ АктивныеНапоминания
	               |ИЗ
	               |	Справочник.НастройкиНапоминаний КАК НастройкиНапоминаний
	               |ГДЕ
	               |	НастройкиНапоминаний.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияНапоминаний.Активно)
	               |	И НастройкиНапоминаний.Периодическое = ИСТИНА
	               |	И НастройкиНапоминаний.КоличествоПовторов > 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	АктивныеНапоминания.Ссылка,
	               |	КОЛИЧЕСТВО(Напоминание.Ссылка) КАК КоличествоНапоминаний,
	               |	МАКСИМУМ(Напоминание.Дата) КАК Дата
	               |ПОМЕСТИТЬ СтатистикаНапоминаний
	               |ИЗ
	               |	АктивныеНапоминания КАК АктивныеНапоминания
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Задача.Напоминание КАК Напоминание
	               |		ПО АктивныеНапоминания.Ссылка = Напоминание.Основание
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	АктивныеНапоминания.Ссылка
	               |
	               |ИМЕЮЩИЕ
	               |	КОЛИЧЕСТВО(Напоминание.Ссылка) < АктивныеНапоминания.Ссылка.КоличествоПовторов - 1
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СтатистикаНапоминаний.Ссылка,
	               |	СтатистикаНапоминаний.Ссылка.КоличествоПовторов КАК КоличествоПовторов,
	               |	СтатистикаНапоминаний.Ссылка.ДатаНачала КАК ДатаНачала,
	               |	СтатистикаНапоминаний.Ссылка.ИнтервалПовторения КАК ИнтервалПовторения,
	               |	СтатистикаНапоминаний.КоличествоНапоминаний КАК КоличествоНапоминаний,
	               |	СтатистикаНапоминаний.Дата
	               |ПОМЕСТИТЬ РасчетныеЗначения
	               |ИЗ
	               |	СтатистикаНапоминаний КАК СтатистикаНапоминаний
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РасчетныеЗначения.Ссылка,
	               |	ВЫБОР
	               |		КОГДА РасчетныеЗначения.Дата ЕСТЬ NULL 
	               |			ТОГДА РасчетныеЗначения.Ссылка.ДатаНачала
	               |		ИНАЧЕ ВЫБОР
	               |				КОГДА РасчетныеЗначения.ИнтервалПовторения = ЗНАЧЕНИЕ(Перечисление.ВидыПериодов.День)
	               |					ТОГДА ДОБАВИТЬКДАТЕ(РасчетныеЗначения.Дата, ДЕНЬ, 1)
	               |				КОГДА РасчетныеЗначения.ИнтервалПовторения = ЗНАЧЕНИЕ(Перечисление.ВидыПериодов.Неделя)
	               |					ТОГДА ДОБАВИТЬКДАТЕ(РасчетныеЗначения.Дата, НЕДЕЛЯ, 1)
	               |				КОГДА РасчетныеЗначения.ИнтервалПовторения = ЗНАЧЕНИЕ(Перечисление.ВидыПериодов.Месяц)
	               |					ТОГДА ДОБАВИТЬКДАТЕ(РасчетныеЗначения.Дата, МЕСЯЦ, 1)
	               |				КОГДА РасчетныеЗначения.ИнтервалПовторения = ЗНАЧЕНИЕ(Перечисление.ВидыПериодов.Год)
	               |					ТОГДА ДОБАВИТЬКДАТЕ(РасчетныеЗначения.Дата, ГОД, 1)
	               |			КОНЕЦ
	               |	КОНЕЦ КАК Дата,
	               |	ВЫБОР
	               |		КОГДА РасчетныеЗначения.КоличествоНапоминаний < РасчетныеЗначения.КоличествоПовторов - 1
	               |			ТОГДА ""Создать""
	               |		ИНАЧЕ ""СоздатьИДеактивировать""
	               |	КОНЕЦ КАК Директива
	               |ПОМЕСТИТЬ КандидатыВСобытия
	               |ИЗ
	               |	РасчетныеЗначения КАК РасчетныеЗначения
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	КандидатыВСобытия.Ссылка КАК Основание,
	               |	КандидатыВСобытия.Дата,
	               |	КандидатыВСобытия.Директива,
	               |	КандидатыВСобытия.Ссылка.СуммаПлатежа КАК СуммаПлатежа,
	               |	КандидатыВСобытия.Ссылка.Наименование КАК Наименование
	               |ИЗ
	               |	КандидатыВСобытия КАК КандидатыВСобытия
	               |ГДЕ
	               |	КандидатыВСобытия.Дата <= &ДатаНапоминания";
	Запрос.УстановитьПараметр("ДатаНапоминания", НачалоДня(ТекущаяДата()) + 60*60*24*Константы.НапоминанияДнейДоСобытия.Получить());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Найти(Выборка.Директива, "Создать") <> 0  Тогда
			Задача = Задачи.Напоминание.СоздатьЗадачу();
			ЗаполнитьЗначенияСвойств(Задача, Выборка);
			Задача.Записать();
		КонецЕсли;
		
		Если Выборка.Директива = "СоздатьИДеактивировать" Тогда
			// пометим напоминание как завершенное
			НапоминаниеОбъект = Выборка.Основание.ПолучитьОбъект();
			НапоминаниеОбъект.Состояние = Перечисления.СостоянияНапоминаний.Завершено;
			НапоминаниеОбъект.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// СоздатьБесконечныеНапоминания - создает задачи на основе периодических напоминаний, бесконечных 
// или с определенной датой окончания
&НаСервере
Процедура СоздатьБесконечныеНапоминания()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	НастройкиНапоминаний.Ссылка КАК Ссылка
	               |ПОМЕСТИТЬ АктивныеНапоминания
	               |ИЗ
	               |	Справочник.НастройкиНапоминаний КАК НастройкиНапоминаний
	               |ГДЕ
	               |	НастройкиНапоминаний.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияНапоминаний.Активно)
	               |	И НастройкиНапоминаний.Периодическое = ИСТИНА
	               |	И НастройкиНапоминаний.КоличествоПовторов = 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	МАКСИМУМ(Напоминание.Дата) КАК Дата,
	               |	АктивныеНапоминания.Ссылка
	               |ПОМЕСТИТЬ ПоследниеНапоминания
	               |ИЗ
	               |	АктивныеНапоминания КАК АктивныеНапоминания
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Задача.Напоминание КАК Напоминание
	               |		ПО (Напоминание.Основание = АктивныеНапоминания.Ссылка)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	АктивныеНапоминания.Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПоследниеНапоминания.Ссылка,
	               |	ПоследниеНапоминания.Ссылка.ИнтервалПовторения КАК ИнтервалПовторения,
	               |	ПоследниеНапоминания.Ссылка.ДатаОкончания КАК ДатаОкончания,
	               |	ПоследниеНапоминания.Дата
	               |ПОМЕСТИТЬ РасчетныеЗначения
	               |ИЗ
	               |	ПоследниеНапоминания КАК ПоследниеНапоминания
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РасчетныеЗначения.Ссылка,
	               |	ВЫБОР
	               |		КОГДА РасчетныеЗначения.Дата ЕСТЬ NULL 
	               |			ТОГДА РасчетныеЗначения.Ссылка.ДатаНачала
	               |		ИНАЧЕ ВЫБОР
	               |				КОГДА РасчетныеЗначения.ИнтервалПовторения = ЗНАЧЕНИЕ(Перечисление.ВидыПериодов.День)
	               |					ТОГДА ДОБАВИТЬКДАТЕ(РасчетныеЗначения.Дата, ДЕНЬ, 1)
	               |				КОГДА РасчетныеЗначения.ИнтервалПовторения = ЗНАЧЕНИЕ(Перечисление.ВидыПериодов.Неделя)
	               |					ТОГДА ДОБАВИТЬКДАТЕ(РасчетныеЗначения.Дата, НЕДЕЛЯ, 1)
	               |				КОГДА РасчетныеЗначения.ИнтервалПовторения = ЗНАЧЕНИЕ(Перечисление.ВидыПериодов.Месяц)
	               |					ТОГДА ДОБАВИТЬКДАТЕ(РасчетныеЗначения.Дата, МЕСЯЦ, 1)
	               |				КОГДА РасчетныеЗначения.ИнтервалПовторения = ЗНАЧЕНИЕ(Перечисление.ВидыПериодов.Год)
	               |					ТОГДА ДОБАВИТЬКДАТЕ(РасчетныеЗначения.Дата, ГОД, 1)
	               |			КОНЕЦ
	               |	КОНЕЦ КАК ДатаСледующегоСобытия,
	               |	РасчетныеЗначения.ДатаОкончания
	               |ПОМЕСТИТЬ КандидатыВСобытия
	               |ИЗ
	               |	РасчетныеЗначения КАК РасчетныеЗначения
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	КандидатыВСобытия.Ссылка КАК Основание,
	               |	КандидатыВСобытия.Ссылка.Наименование КАК Наименование,
	               |	КандидатыВСобытия.Ссылка.СуммаПлатежа КАК СуммаПлатежа,
	               |	КандидатыВСобытия.ДатаСледующегоСобытия КАК Дата,
	               |	ВЫБОР
	               |		КОГДА КандидатыВСобытия.ДатаСледующегоСобытия <= ВЫБОР
	               |				КОГДА КандидатыВСобытия.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
	               |					ТОГДА ДАТАВРЕМЯ(3000, 1, 1)
	               |			КОНЕЦ
	               |			ТОГДА ""Создать""
	               |		ИНАЧЕ ""Деактивировать""
	               |	КОНЕЦ КАК Директива
	               |ИЗ
	               |	КандидатыВСобытия КАК КандидатыВСобытия
	               |ГДЕ
	               |	КандидатыВСобытия.ДатаСледующегоСобытия <= &ДатаНапоминания";
	Запрос.УстановитьПараметр("ДатаНапоминания", НачалоДня(ТекущаяДата()) + 60*60*24*Константы.НапоминанияДнейДоСобытия.Получить());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Директива = "Создать" Тогда
			Задача = Задачи.Напоминание.СоздатьЗадачу();
			ЗаполнитьЗначенияСвойств(Задача, Выборка);
			Задача.Записать();
		Иначе
			// пометим напоминание как завершенное
			НапоминаниеОбъект = Выборка.Основание.ПолучитьОбъект();
			НапоминаниеОбъект.Состояние = Перечисления.СостоянияНапоминаний.Завершено;
			НапоминаниеОбъект.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры


